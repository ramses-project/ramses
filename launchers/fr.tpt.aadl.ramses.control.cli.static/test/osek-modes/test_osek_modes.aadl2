--
-- AADL-RAMSES
-- 
-- Copyright Â¬Â© 2012 TELECOM ParisTech and CNRS
-- 
-- TELECOM ParisTech/LTCI
-- 
-- Authors: see AUTHORS
-- 
-- This program is free software: you can redistribute it and/or modify 
-- it under the terms of the Eclipse Public License as published by Eclipse,
-- either version 1.0 of the License, or (at your option) any later version.
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
-- Eclipse Public License for more details.
-- You should have received a copy of the Eclipse Public License
-- along with this program.  If not, see 
-- http://www.eclipse.org/org/documents/epl-v10.php
--

package test_osek_modes
public
with OSEK, Data_Model;
  system root
  end root;

  system implementation root.impl
  subcomponents
    the_cpu: processor cpu.impl;
    the_proc_modes: process proc1.impl;
    the_mem: memory mem;
  properties
    actual_processor_binding => (reference (the_cpu)) applies to the_proc_modes;
    actual_memory_binding => (reference(the_mem)) applies to the_proc_modes;
  end root.impl;
  
  memory mem
  end mem;
  
  processor cpu
  properties
      Scheduling_Protocol => (RMS) ;
      OSEK::SystemCounter_MaxAllowedValue => 2000;
      OSEK::SystemCounter_TicksPerBase => 1;
      OSEK::SystemCounter_MinCycle => 1;
  end cpu;

  processor implementation cpu.impl
  end cpu.impl;

  process proc1
  end proc1;

  process implementation proc1.impl
  subcomponents
    the_sender: thread sender.impl;
    the_receiver: thread receiver.impl;
    the_receiver_m1: thread receiver.impl in modes (m1);
    the_receiver_m2: thread receiver.impl in modes (m2);
    c11: data Integer {Data_Model::Initial_Value=>("11");};
    c22: data Integer {Data_Model::Initial_Value=>("22");};
  connections
--    cnx10: port the_sender.p -> the_receiver_m1.mode_switch;
--    cnx12: port the_sender.p -> the_receiver_m2.mode_switch;
    cnx2: port c11 -> the_receiver_m1.input_data in modes (m1);
    cnx3: port c22 -> the_receiver_m2.input_data in modes (m2);
    cnx_m1: port c11 -> the_receiver.input_data in modes (m1);
    cnx_m2: port c22 -> the_receiver.input_data in modes (m2);
  modes
    m1: initial mode;
    m2: mode;
    m1 -[the_sender.p]-> m2;
    m2 -[the_sender.p]-> m1;
  end proc1.impl;

  thread sender
  features
    p: out event port;
  properties
    Dispatch_Protocol => Periodic;
    Compute_Execution_Time => 0 ms .. 1 ms;
    Period => 1000 Ms;
    Priority => 10;
    Data_Size => 4000 bytes;
    Stack_Size => 512 bytes;
    Code_Size => 40 bytes;
  end sender;

  thread implementation sender.impl
  calls
    call : { c : subprogram sender_spg;};
  connections
    cnx: port c.result -> p;
  properties
    Compute_Entrypoint_Call_Sequence => reference (call);
  end sender.impl;

  subprogram sender_spg
  features
    result : out event port;
  properties
    source_name => "send";
    source_language => (C);
    source_text => ("user_send.h","user_send.c");
  end sender_spg;

  thread receiver
  features
--    mode_switch: in event port {Queue_Size => 5; 
--    					Queue_Processing_Protocol => FIFO;};
    input_data: in data port Integer;
  properties
    Dispatch_Protocol => Periodic;
    Compute_Execution_Time => 0 ms .. 1 ms;
    Period => 2000 Ms;
    Priority => 5;
    Data_Size => 4000 bytes;
    Stack_Size => 512 bytes;
    Code_Size => 40 bytes;
  end receiver;

  thread implementation receiver.impl
  calls
    call : { 
    	c1 : subprogram receiver_spg;
    };
  connections
    cnx: parameter input_data -> c1.input_data;
  properties
    Compute_Entrypoint_Call_Sequence => reference (call);
  end receiver.impl;

  subprogram receiver_spg
  features
    input_data: in parameter Integer;
  properties
    source_name => "receive";
    source_language => (C);
    source_text => ("user_receive.h","user_receive.c");
  end receiver_spg;

  data Integer
  properties
    Data_Model::Data_Representation => integer;
  end Integer;

end test_osek_modes;
