package Minimal_Runtime_Types

public 

with Data_Model, Base_Types ;


data Date
end Date ;


data Byte

   properties
   
   Source_Data_Size => 1 Bytes;

end Byte ;


data Buffer

   properties
   
   Data_Model::Data_Representation => Array ;
   Data_Model::Base_Type => (classifier (Byte)) ;
   Data_Model::Dimension => (0) ; -- TODO temporary.
--   Data_Model::Dimension => (Indefinite_Dimension) ; -- To be refined.

end Buffer ;


data implementation Buffer.impl

   subcomponents
   
   Length : data Base_Types::Natural {Data_Model::Initial_Value => ("0");} ;

end Buffer.impl ;


feature group Link_Access

   features
   
   Next : provides data access Pending_Event_Link.impl  ;
   Previous : provides data access Pending_Event_Link.impl ; 

end Link_Access ;


data Pending_Event_Link

   features
   
   Cpt_Access : feature group Link_Access ;

end Pending_Event_Link ;


data implementation Pending_Event_Link.impl

   subcomponents
   
   Port_Id : data Base_Types::Integer_16 {Data_Model::Initial_Value => ("-1");} ; -- -1 for the first or the last link.
   Timestamp : data Date {Data_Model::Initial_Value => ("0");} ;
   Is_Connected : data Base_Types::Boolean {Data_Model::Initial_Value => ("FALSE");} ;

end Pending_Event_Link.impl ;


data Item
end Item ;


data implementation Item.impl

   prototypes
   
   Port_Type : data ;
   
   subcomponents
   
   Payload : data Port_Type ;
   Link : data Pending_Event_Link.impl ;
   
end Item.impl ;


data Pending_Event_Chain
end Pending_Event_Chain ;


data implementation Pending_Event_Chain.impl

   subcomponents
   
   First : data Pending_Event_Link.impl ;
   Last : data Pending_Event_Link.impl ;
      
end Pending_Event_Chain.impl ;


data Item_Array

   prototypes
   
   Item_Type : data Item.impl ;
   
   properties
   
   Data_Model::Data_Representation => Array ;
   -- OSATE doesn't support data prototype has base type property value.
   Data_Model::Base_Type => (classifier (Item.impl)) ; -- TEMPORARY. Normaly, it is Item_Type.
   Data_Model::Dimension => (0) ; -- TODO temporary.
   -- Data_Model::Dimension => (Indefinite_Dimension) ; -- Has to be refined.

end Item_Array ;


data Queue
end Queue ;


data implementation Queue.impl

   prototypes
   
   Item_Array_Type : data Item_Array ;
   Item_Type : data Item.impl ;
   
   subcomponents
   
   Array : data Item_Array_Type ;
   Last_Value : data Item_Type ;
   First : data Base_Types::Natural {Data_Model::Initial_Value => ("0");} ; -- Initialized to zero.
   Last : data Base_Types::Natural {Data_Model::Initial_Value => ("0");} ; -- Initialized to zero.
   Is_Empty : data Base_Types::Boolean {Data_Model::Initial_Value => ("FALSE");} ; -- Initialized to FALSE.
   Length : data Base_Types::Natural {Data_Model::Initial_Value => ("0");} ; -- Initialized at the thread's initialization.
   
end Queue.impl ;


data Event_Port_Variable
end Event_Port_Variable ;


data implementation Event_Port_Variable.impl ---------

   prototypes
   
   Queue_Type : data Queue.impl ;
      
   subcomponents
   
   Id : data Base_Types::Unsigned_8 {Data_Model::Initial_Value => ("0");} ;
   Queue : data Queue_Type ;
   Dequeuable_Items : data Base_Types::Natural {Data_Model::Initial_Value => ("0");} ; -- Reset at the begining of the dispatch time. Zero based counter of dequeued items.
   Readable_Items : data Base_Types::Natural {Data_Model::Initial_Value => ("1");} ; -- ie Dequeue_Protocol property. Set queue's length for the AllItems property value. Initialized at the thread's initialization.
   Is_Updated : data Base_Types::Boolean [2] {Data_Model::Initial_Value => ("FALSE");} ; -- port update flag.
   
end Event_Port_Variable.impl ;


data Data_Port_Item_Array extends Item_Array

   properties
   
   Data_Model::Dimension => (2) ;

end Data_Port_Item_Array ;


data Data_Port_Variable
end Data_Port_Variable ;


data implementation Data_Port_Variable.impl

   prototypes
   
   Item_Array_Type : data Data_Port_Item_Array ;

   subcomponents
   
   Id : data Base_Types::Unsigned_8 {Data_Model::Initial_Value => ("0");} ;
   Array : data Item_Array_Type ;
   Snapshot_Index : data Base_Types::Unsigned_8 {Data_Model::Initial_Value => ("0");} ;
   Stream_Index : data Base_Types::Unsigned_8 {Data_Model::Initial_Value => ("1");} ;
   Is_Fresh : data Base_Types::Boolean {Data_Model::Initial_Value => ("FALSE");} ;

end Data_Port_Variable.impl ;


data Generic_List

   properties
   
   Data_Model::Data_Representation => Array ;
   Data_Model::Dimension => (0) ; -- TODO temporary.
   -- Data_Model::Dimension => (Indefinite_Dimension) ; -- Has to be refined.

end Generic_List ;
  

data implementation Generic_List.impl

  subcomponents
   
   Nb_Items : data Base_Types::Natural {Data_Model::Initial_Value => ("0");} ;
   Length : data Base_Types::Natural {Data_Model::Initial_Value => ("0");} ;

end Generic_List.impl ;


data Natural_List extends Generic_List
end Natural_List ;


data implementation Natural_List.impl extends Generic_List.impl

   properties
   
   Data_Model::Base_Type => (classifier (Base_Types::Unsigned_8)) ;

end Natural_List.impl ;


data Pending_Event_Chain_List extends Generic_List
end Pending_Event_Chain_List ;


data implementation Pending_Event_Chain_List.impl extends Generic_List.impl

   properties
   
   Data_Model::Base_Type => (classifier (Pending_Event_Chain.impl)) ;

end Pending_Event_Chain_List.impl ;


end Minimal_Runtime_Types ;
