--
-- AADL-RAMSES
-- 
-- Copyright Â© 2012 TELECOM ParisTech and CNRS
-- 
-- TELECOM ParisTech/LTCI
-- 
-- Authors: see AUTHORS
-- 
-- This program is free software: you can redistribute it and/or modify 
-- it under the terms of the Eclipse Public License as published by Eclipse,
-- either version 1.0 of the License, or (at your option) any later version.
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
-- Eclipse Public License for more details.
-- You should have received a copy of the Eclipse Public License
-- along with this program.  If not, see 
-- http://www.eclipse.org/org/documents/epl-v10.php
--

-- @nsURI AADLI=http:///AADL2/instance

module PropertiesTools;

create 
	OUT 				   : AADLBA
from 
	IN 					   : AADLI,
	BASE_TYPES			   : AADLBA,
	AADL_RUNTIME		   : AADLBA,
	DATA_MODEL			   : AADLBA,
	SCHEDULER_CONSTANTS	   : AADLBA,
	SCHEDULER_REALIZATIONS : AADLBA,
	SCHEDULER_RUNTIME	   : AADLBA;

uses AADLCopyHelpers;
uses AADLICopyHelpers;
uses FHSHelpers;
uses AADLRuntimeHelpers;
uses Services;

helper context String def : asBaseType() : AADLBA!Classifier =
	AADLBA!Classifier->allInstancesFrom('BASE_TYPES')->any(c|c.name=self)
;

helper context String def : asDataType(model: String) : AADLBA!Classifier =
	AADLBA!Classifier->allInstancesFrom(model)->any(c|c.name=self)
;

helper context String def : asSubprogramType(model: String) : AADLBA!Classifier =
	AADLBA!Classifier->allInstancesFrom(model)->any(c|c.name=self)
;

helper context String def : asProperty(model : String) : AADLBA!Property =
	AADLBA!Property->allInstancesFrom(model)->any(p|p.name=self)
;

helper context String def : asEnumeration(model : String) : AADLBA!EnumerationLiteral =
	AADLBA!EnumerationLiteral->allInstancesFrom(model)->any(e|e.name=self)
;

helper context String def : asClassifier(model : String) : AADLBA!Classifier =
	AADLBA!Classifier->allInstancesFrom(model)->any(p|p.name=self)
;

helper def : getProperty(propertyName : String) : AADLBA!Property =
	AADLBA!PropertySet->allInstances()->collect(ps|ps.ownedProperty)
						->flatten()->any(p|p.name.toLower()=propertyName.toLower())
;

helper context AADLBA!NamedElement def : getPropertyAssociation(propertyType : String) : AADLBA!PropertyAssociation =
	let propertyAssociations : Sequence(AADLBA!PropertyAssociation) = self.ownedPropertyAssociation.debug(self.name+' available property associations') in
	propertyAssociations->select(p | p.isIdentifiedPropertyAssociation(propertyType.toLower())=true)->first();
	
helper context AADLBA!NamedElement def : getModalPropertyValue(propertyType : String) : AADLBA!ModalPropertyValue =
	if(not self.getPropertyAssociation(propertyType).oclIsUndefined()) then
		self.getPropertyAssociation(propertyType).ownedValue->first()
	else
		if(self.oclIsTypeOf(AADLBA!ComponentImplementation)) then
			self.type.getPropertyAssociation(propertyType).ownedValue->first()
		else
			OclUndefined
		endif
	endif
;

helper context AADLBA!PropertyAssociation def : isIdentifiedPropertyAssociation(identifier:String): Boolean =
	if(self.property.oclIsUndefined() or self.property.name.oclIsUndefined()) then
		false.debug('UNDEFINED OF UNAMED')
	else 
		if (self.property.name.toLower() = identifier.toLower()) then
			true.debug('IN PROPERTYASSOCIATION IDENTIFICATION')
		else
			false.debug('NOT IDENTIFIED')
		endif
	endif;

rule CreatePropertyAssociation (propertyName : String, model : String, value : AADLBA!PropertyExpression) {
	to
		p : AADLBA!PropertyAssociation (
			property <- propertyName.asProperty(model),
			ownedValue <- Sequence {mpv}
		),
		
		mpv : AADLBA!ModalPropertyValue (
			ownedValue <- value
		)
	do { p; }
}

rule CreateStringLiteralPropertyExpression (value : String) {
	to
		pr : AADLBA!StringLiteral(value<-value)
	do
	{
		pr;
	}
}

rule CreateIntegerLiteralPropertyExpression (value: Integer)
{
	to
		pr : AADLBA!IntegerLiteral(value<-value)
	do
	{
		pr;
	}
}

rule CreateListValueFromInteger (value: Integer)
{
	to
		il : AADLBA!IntegerLiteral(value<-value),
		lv : AADLBA!ListValue(ownedListElement<-Sequence{il})
	do
	{
		lv;
	}
}

rule CreateListValueFromClassifier (value: AADLBA!Classifier)
{
	to
		cv : AADLBA!ClassifierValue(classifier<-value),
		lv : AADLBA!ListValue(ownedListElement<-Sequence{cv})
	do
	{
		lv;
	}
}