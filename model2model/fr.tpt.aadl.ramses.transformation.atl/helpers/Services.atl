--
-- AADL-RAMSES
-- 
-- Copyright Â© 2012 TELECOM ParisTech and CNRS
-- 
-- TELECOM ParisTech/LTCI
-- 
-- Authors: see AUTHORS
-- 
-- This program is free software: you can redistribute it and/or modify 
-- it under the terms of the Eclipse Public License as published by Eclipse,
-- either version 1.0 of the License, or (at your option) any later version.
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
-- Eclipse Public License for more details.
-- You should have received a copy of the Eclipse Public License
-- along with this program.  If not, see 
-- http://www.eclipse.org/org/documents/epl-v10.php
--

-- @atlcompiler emftvm

library Services;

helper context AADLI!Element def: getInstanceObjectPathIfNE() : String = 
	if(self.oclIsKindOf(AADLI!InstanceObject)) then
		self.getInstanceObjectPath()
	else
		if(self.oclIsKindOf(AADLI!NamedElement)) then
			self.getQualifiedName()
		else
			'NaNE'
		endif
	endif
;

--- Returns the same as thisModule.resolveTemp(var : OclAny, rule_name : String, target_pattern_name : String),
--- except that it returns OclUndefined if no trace exists instead of throwing an error.
helper def : resolveLazyTupleSingleIfExists(var : Sequence(OclAny), rule_name : String, target_pattern_name : String) : OclAny =
    let tr : OclAny = thisModule.traces.getLinksByRule(rule_name, false) in
    if tr.oclIsUndefined() then
    	OclUndefined
    else
    	let sel : OclAny = tr.getUniqueSourceElements(var) in
        if sel.oclIsUndefined() then
        	OclUndefined
        else
        	let te : OclAny = sel.getSourceElements().get(0).getSourceOf().getTargetElement(target_pattern_name) in
            if te.oclIsUndefined() then
            	OclUndefined
            else
            	te.getObject()
            endif
        endif
       endif;

helper def : resolveLazySingleIfExists(var :OclAny, rule_name : String, target_pattern_name : String) : OclAny =
    let tr : OclAny = thisModule.traces.getLinksByRule(rule_name, false) in
    if tr.oclIsUndefined() then
    	OclUndefined
    else
    	let sel : OclAny = tr.getUniqueSourceElement(var) in
        if sel.oclIsUndefined() then
        	OclUndefined
        else
        	let te : OclAny = sel.getSourceOf().getTargetElement(target_pattern_name) in
            if te.oclIsUndefined() then
            	OclUndefined
            else
            	te.getObject()
            endif
        endif
       endif;
        
helper def : resolveMatchedTupleIfExists(var : Sequence(OclAny), target_pattern_name : String) : OclAny =
   	let sel : OclAny = thisModule.traces.getDefaultSourceElements(var) in
       if sel.oclIsUndefined() then
       	OclUndefined
       else
       	let te : OclAny = sel.getSourceElements().get(0).getSourceOf().getTargetElement(target_pattern_name) in
           if te.oclIsUndefined() then
           	OclUndefined
           else
           	te.getObject()
           endif
      endif;

helper def : resolveMatchedSingleIfExists(var : OclAny, target_pattern_name : String) : OclAny =
   	let sel : OclAny = thisModule.traces.getDefaultSourceElement(var) in
       if sel.oclIsUndefined() then
       	OclUndefined
       else
       	let te : OclAny = sel.getSourceOf().getTargetElement(target_pattern_name) in
           if te.oclIsUndefined() then
           	OclUndefined
           else
           	te.getObject()
           endif
      endif;

helper context OclAny def : debug(msg: String) : OclAny =
	thisModule.theHOOK.log(msg, self)
;

helper context OclAny def: allInstances(ele: AADLI!Element) : Sequence(OclAny) =
	thisModule.theHOOK.allInstances(ele, self)
;
       
helper def: dataAccessSuffix(): String ='_PortIdAccess_';
helper def: callPrefix(): String ='call';

helper def: theHOOK : ATLHOOKS!HookAccess = ATLHOOKS!HookAccess.allInstancesFrom('HOOKS')->first();

helper def: getAllInstances(category: String) : Sequence(AADLI!ComponentInstance) = 
	AADLI!ComponentInstance.allInstancesFrom('IN')->select(e|e.category.getName()=category);


helper context AADLBA!ThreadImplementation def : callSequence() : AADLBA!SubprogramCallSequence =
	self.ownedSubprogramCallSequence->first()
;

helper context AADLI!SubprogramCallSequence def: isComputeEntryPointOf(c: AADLI!InstanceObject): Boolean =
	c.ownedPropertyAssociation->exists(
				e | 
				e.property.name = 'Compute_Entrypoint_Call_Sequence'
				and
				e.ownedValue->exists(f|f.ownedValue.containmentPathElement->first().namedElement=self)
			)
;

helper context AADLI!SubprogramCallSequence def: modesIsComputeEntryPointOf(c: AADLI!InstanceObject): Sequence(AADLBA!Mode) =
	c.ownedPropertyAssociation->select(
				e | 
				e.property.name = 'Compute_Entrypoint_Call_Sequence'
				and
				e.ownedValue->exists(f|f.ownedValue.containmentPathElement->first().namedElement=self)
			)
			->collect(
				e | e.ownedValue->collect(ov | ov.inMode)
			)
			->flatten()
			->asSet()
;

helper context AADLI!InstanceObject def: getComputeEntryPoints(): Sequence(AADLI!SubprogramCallSequence) =
	self.ownedPropertyAssociation->select(
				e | 
				e.property.name = 'Compute_Entrypoint_Call_Sequence'
			)->collect
			(
				owned_val| owned_val.ownedValue->collect(val|val.ownedValue.containmentPathElement->first().namedElement)
			)->flatten()
;


helper context AADLBA!DirectedFeature def: isOutputFeature(): Boolean =
	self.getDirection() = #out;
		
helper context AADLBA!DirectedFeature def: isInputOutputFeature(): Boolean =
	self.getDirection() = #inOut;

helper context AADLBA!DirectedFeature def: isInputFeature(): Boolean =
	(not self.isOutputFeature() and not self.isInputOutputFeature());

helper context AADLI!FeatureInstance def: isOutputFeature(): Boolean =
	self.feature.oclIsKindOf(AADLBA!DirectedFeature) and self.direction = #out;
		
helper context AADLI!FeatureInstance def: isInputOutputFeature(): Boolean =
	self.feature.oclIsKindOf(AADLBA!DirectedFeature) and self.direction = #inOut;

helper context AADLI!FeatureInstance def: isInputFeature(): Boolean =
	(self.feature.oclIsKindOf(AADLBA!DirectedFeature) and not self.isOutputFeature() and not self.isInputOutputFeature());

helper context AADLBA!SubprogramType def: getOrderedFeature() : Sequence(AADLBA!Feauture) =
	if(self.oclIsUndefined()) then
		OclUndefined
	else
		ATLHOOKS!HookAccess.allInstancesFrom('HOOKS')->first().orderFeatures(self)
	endif
;

helper context AADLBA!SubprogramImplementation def: getOrderedFeature() : Sequence(AADLBA!Feauture) =
	self.getComponentType().getOrderedFeature();

helper context AADLBA!ComponentImplementation def: getComponentType(): AADLBA!ComponentType = 
	self.type;

helper context AADLI!SubcomponentType def : getBA() : AADLBA!BehaviorAnnex = 
	let as: AADLI!AnnexSubclause = self.ownedAnnexSubclause->any(e | e.name='behavior_specification') in
	if(as.oclIsKindOf(AADLBA!BehaviorAnnex)) then
		as
	else 
		if(not as.oclIsUndefined()) then
			as.parsedAnnexSubclause
		else
			OclUndefined
		endif
	endif
;

helper context AADLBA!SubprogramSubcomponentType def: getSubprogramType() : AADLBA!SubprogramType = 
	if(self.oclIsKindOf(AADLBA!SubprogramType)) then
		self
	else 
		if(self.oclIsKindOf(AADLBA!SubprogramImplementation)) then
			self.type
		else
			OclUndefined
		endif
	endif
;

helper context AADLBA!SubprogramType def: getSubprogramParameters() : Sequence(AADLBA!Parameter) = 
	self.ownedParameter;

helper context AADLBA!SubprogramImplementation def: getSubprogramParameters() : Sequence(AADLBA!Parameter) = 
	self.type.ownedParameter;

helper context AADLI!Feature def : isSubprogramPort() : Boolean =
	self.eContainer().oclIsKindOf(AADLBA!Subprogram) and self.oclIsKindOf(AADLBA!Port)
;

helper context AADLI!Feature def : isThreadPort() : Boolean =
	self.eContainer().oclIsKindOf(AADLBA!Thread) and self.oclIsKindOf(AADLBA!Port)
;

helper context AADLI!FeatureInstance def : isThreadPort() : Boolean =
	self.getComponentInstance().category = #thread and self.feature.oclIsKindOf(AADLBA!Port)
;

helper context AADLI!ComponentInstance def : isThreadPort() : Boolean =
	false
;

helper context AADLI!Feature def : isProcessPort() : Boolean =
	self.eContainer().oclIsKindOf(AADLBA!Process) and self.oclIsKindOf(AADLBA!Port)
;

helper context AADLI!FeatureInstance def : isProcessPort() : Boolean =
	self.getComponentInstance().category=#process  and self.feature.oclIsKindOf(AADLBA!Port)
;

helper context AADLI!ComponentInstance def : isProcessPort() : Boolean =
	false
;

helper context AADLI!FeatureInstance def : isDataOrEventDataPort() : Boolean =
	(self.isEventDataPort() or self.isDataPort()); 

helper context AADLI!FeatureInstance def : isEventOrEventDataPort() : Boolean =
	(self.isEventDataPort() or self.isEventPort());

helper context AADLI!FeatureInstance def : isEventPort(): Boolean =
	self.feature.oclIsTypeOf(AADLI!EventPort);

helper context AADLI!FeatureInstance def : isEventDataPort(): Boolean =
	self.feature.oclIsTypeOf(AADLI!EventDataPort);

helper context AADLI!FeatureInstance def : isDataPort(): Boolean =
	self.feature.oclIsTypeOf(AADLI!DataPort);

helper context AADLI!FeatureInstance def : connectedToDataComponent(): Boolean =
	(not self.srcConnectionInstance->isEmpty() and self.srcConnectionInstance->size()=1 and self.srcConnectionInstance->first().destination.oclIsTypeOf(AADLI!ComponentInstance))
	 or
	(not self.dstConnectionInstance->isEmpty() and self.dstConnectionInstance->size()=1 and self.dstConnectionInstance->first().source.oclIsTypeOf(AADLI!ComponentInstance))
;

helper context AADLI!FeatureInstance def : connectedToDataComponentInMode(mode: AADLI!Mode): Boolean = 
	self.srcConnectionInstance->select(e|e.connectionReference->exists(f|f.connection.inModeOrTransition->includes(mode)))->collect(e|e.destination)
	->union(	
		self.dstConnectionInstance->select(e|e.connectionReference->exists(f|f.connection.inModeOrTransition->includes(mode)))->collect(e|e.source)
	)
	->exists(e|e.oclIsTypeOf(AADLI!ComponentInstance) and e.category = #data)
;

helper context AADLI!FeatureInstance def : connectedDataComponent(): AADLI!ComponentInstance =
	if(not self.srcConnectionInstance->isEmpty() and self.srcConnectionInstance->first().destination.oclIsTypeOf(AADLI!ComponentInstance)) then
		self.srcConnectionInstance->first().destination
	else
		self.dstConnectionInstance->first().source
	endif
;



helper context AADLI!FeatureInstance def : getConnectedDataComponent() : Sequence(AADLI!ComponentInstance) =
	if(not self.srcConnectionInstance->isEmpty()) then
		self.srcConnectionInstance->select(e|e.destination.oclIsTypeOf(AADLI!ComponentInstance))
	else
		if(not self.dstConnectionInstance->isEmpty()) then
			self.dstConnectionInstance->select(e|e.source.oclIsTypeOf(AADLI!ComponentInstance))
		else
			Sequence{}
		endif
	endif
;

helper context AADLI!FeatureInstance def : getConnectionEndInMode(mode: AADLI!Mode): Sequence(AADLI!ComponentInstance) = 
	self.srcConnectionInstance->select(e|e.connectionReference->exists(f|f.connection.inModeOrTransition->includes(mode)))->collect(e|e.destination)
	->union(	
		self.dstConnectionInstance->select(e|e.connectionReference->exists(f|f.connection.inModeOrTransition->includes(mode)))->collect(e|e.source)
	)
;
		
helper context AADLI!FeatureInstance def : isConnectedToDataComponent(data: AADLI!ComponentInstance) : Boolean =
	(not self.srcConnectionInstance->isEmpty() and self.srcConnectionInstance->exists(e|e.destination = data))
	or
	(not self.dstConnectionInstance->isEmpty() and self.dstConnectionInstance->exists(e|e.source = data))
;

helper context AADLI!FeatureInstance def : connectedInMode(mode: AADLI!Mode) : Boolean =
--	let remainingModes: Sequence(AADLI!Mode) = self.getComponentInstance().inMode->select(e|not (e.mode=mode)) in
--	if(self.getComponentInstance().inMode->isEmpty()
--			or not remainingModes->isEmpty())
--	then
		self.srcConnectionInstance->exists(e|e.connectionReference->exists(f|f.connection.inModeOrTransition->includes(mode)))
		or
		self.dstConnectionInstance->exists(e|e.connectionReference->exists(f|f.connection.inModeOrTransition->includes(mode)))
--	else
--		false
--	endif
;

helper context AADLI!FeatureInstance def : isIntraProcessFeatureInstance(): Boolean =
	not self.srcConnectionInstance->exists(e| not (e.destination.getProcess() = self.getProcess()))
	and
	not self.dstConnectionInstance->exists(e| not (e.source.getProcess() = self.getProcess()))
;


helper context AADLI!InstanceObject def : getProcess(): AADLI!ComponentInstance =
	if(self.getComponentInstance().category=#process) then
		self.getComponentInstance()
	else
			self.getComponentInstance().getProcess()
	endif
;

helper context AADLI!ComponentInstance def : getProcess(): AADLI!ComponentInstance =
	if(self.category=#process) then
		self
	else
		self.eContainer().getProcess()
	endif
;


helper context AADLI!InstanceObject def: getRemoteConnections():  Sequence(AADLI!ConnectionInstance) =
	AADLI!ConnectionInstance.allInstancesFrom('IN')->select(f |
    	self.isPartOf(f) and f.isRemoteConnection())
;

helper context AADLI!ConnectionInstance def: isRemoteConnection() : Boolean =
	not self.isIntraProcessorConnection()
;

helper context AADLI!InstanceObject def: isIntraProcessorConnection() : Boolean =
	self.source.getProcess().getProcessorBinding()->first()=self.destination.getProcess().getProcessorBinding()->first()
;
	
helper context AADLI!NamedElement def: getPeriodValue(): Integer =
	let mpv: AADLBA!ModalPropertyValue = self.getModalPropertyValue('Period') in
	if(mpv.oclIsUndefined()) then
		false
	else
		mpv.ownedValue.value
	endif
;

helper context AADLI!NamedElement def: isPeriodicThread() : Boolean =
	let mpv: AADLBA!ModalPropertyValue = self.getModalPropertyValue('Dispatch_Protocol') in
	if(mpv.oclIsUndefined()) then
		false
	else
		mpv.ownedValue.namedValue.name.toLower() = 'periodic'
	endif
;

helper context AADLI!NamedElement def: isSporadicThread() : Boolean =
	let mpv: AADLBA!ModalPropertyValue = self.getModalPropertyValue('Dispatch_Protocol') in
	if(mpv.oclIsUndefined()) then
		false
	else
		mpv.ownedValue.namedValue.name.toLower() = 'sporadic'
	endif
;

helper context AADLI!NamedElement def: isAperiodicThread() : Boolean =
	let mpv: AADLBA!ModalPropertyValue = self.getModalPropertyValue('Dispatch_Protocol') in
	if(mpv.oclIsUndefined()) then
		false
	else
		mpv.ownedValue.namedValue.name.toLower() = 'aperiodic'
	endif
;

helper context AADLI!NamedElement def: isTimedThread() : Boolean =
	let mpv: AADLBA!ModalPropertyValue = self.getModalPropertyValue('Dispatch_Protocol') in
	if(mpv.oclIsUndefined()) then
		false
	else
		mpv.ownedValue.namedValue.name.toLower() = 'timed'
	endif
;

helper context AADLI!NamedElement def: isHybridThread() : Boolean =
	let mpv: AADLBA!ModalPropertyValue = self.getModalPropertyValue('Dispatch_Protocol') in
	if(mpv.oclIsUndefined()) then
		false
	else
		mpv.ownedValue.namedValue.name.toLower() = 'hybrid'
	endif
;

helper context AADLI!FeatureInstance def: isPeriodicThreadPort(): Boolean =
	self.isThreadPort() and self.eContainer().isPeriodicThread()
;

helper context AADLI!ComponentInstance def: isPeriodicThreadPort(): Boolean =
	false
;

helper context AADLI!ConnectionInstance def: isIntraProcessConnection() : Boolean =
	self.source.eContainer().eContainer() = self.destination.eContainer().eContainer() 
;


helper context AADLI!ComponentInstance def: getPeriod() : Integer = 
	-- should call java code (PropertyUtils class) instead
	self.getModalPropertyValue('Period').ownedValue.value
;

helper context AADLI!ComponentInstance def: getPeriodUnit(): AADLI!UnitLiteral =
	self.getModalPropertyValue('Period').ownedValue.unit
;
	
helper context AADLI!ComponentInstance def: getDeadline() : Integer = 
	-- should call java code (PropertyUtils class) instead
	if(self.getModalPropertyValue('Deadline').oclIsUndefined()) then
		self.getModalPropertyValue('Period').ownedValue.value
	else
		self.getModalPropertyValue('Deadline').ownedValue.value
	endif
;

helper def: getBAInitialStateIdentifier(implBA: AADLBA!BehaviorAnnex) : String =
	implBA.states->any(s|s.initial=true).name
;

helper context AADLI!Connection def : uniqueName(): String =
	let component : AADLI!ComponentImplementation = self.eContainer() in
	if(not self.name.isEmpty()) then
		self.name
	else
		'cnx_generated_id_'+component.ownedConnection.indexOf(self).toString()
	endif
;

helper context AADLI!ComponentInstance def : getActualProcessorBinding()  : Sequence(AADLI!ComponentInstance) =
	self.ownedPropertyAssociation->any(e | e.property.name = 'Actual_Processor_Binding').ownedValue->first().ownedValue.ownedListElement
	->collect(e|e.referencedInstanceObject)
;
	
helper context AADLI!ComponentInstance def : getProcessorBinding() : Sequence(AADLI!ComponentInstance) =
	self.getActualProcessorBinding()->collect(e|e.getProcessorContainer())
;

helper context AADLI!ComponentInstance def: getProcessorContainer() : AADLI!ComponentInstance =
	if self.category=#processor then
		self
	else
		if self.eContainer().oclIsUndefined() then
			OclUndefined
		else
			self.eContainer().getProcessorContainer()
		endif
	endif
;

helper context AADLI!FeatureInstance def: getInputMessagePortId(): Integer = 
	self.getComponentInstance().featureInstance->select(e| e.isInputFeature() and 
										(self.category=#eventDataPort or self.category=#eventPort)
															)->indexOf(self)
;

helper context AADLI!ComponentInstance def: getContainingThread():AADLBA!ThreadClassifier =
	if(self.eContainer().category=#thread) then
		self.eContainer()
	else
		self.eContainer().getContainingThread()
	endif
;

helper def : collectPeriodicDelayed_GlobalVariables(c: AADLI!ComponentInstance): Sequence(AADLBA!DataSubcomponent) =
	Sequence{}
;

helper def : collectPeriodicDelayed_GlobalVariablesAccesses(c: AADLI!ComponentInstance): Sequence(AADLBA!DataAccess) =
	Sequence{}
;

helper def : collectPeriodicDelayed_GlobalVariablesConnections(c: AADLI!ComponentInstance): Sequence(AADLBA!DataAccess) =
	Sequence{}
;

helper def : collectPeriodicDelayed_LocalVariablesAccesses(c: AADLI!ComponentInstance, behavior: AADLI!Element): Sequence(AADLBA!DataAccess) =
	Sequence{}
;

helper def : collectPeriodicDelayed_LocalVariablesConnections(c: AADLI!ComponentInstance, behavior: AADLI!Element): Sequence(AADLBA!DataAccess) =
	Sequence{}
;

helper def: collectPeriodicDelayed_LocalVariables(c: AADLI!ComponentInstance, behavior: Sequence(AADLI!SubprogramCallSequence)): Sequence(AADLBA!DataSubcomponent)=
	Sequence{}
;

helper def: collectPeriodicDelayed_ThreadVariables(c: AADLI!ComponentInstance): Sequence(AADLBA!DataSubcomponent)=
	Sequence{}
;

helper def: collectInitCallSequence(c: AADLI!ComponentInstance): AADLBA!SubprogramCallSequence =
	let mpv: AADLBA!ModalPropertyValue = c.getModalPropertyValue('Initialize_Entrypoint_Call_Sequence') in
	if(mpv.oclIsUndefined()) then
		OclUndefined
	else
		mpv.ownedValue.containmentPathElement->first().namedElement
	endif
;

helper context AADLI!FeatureInstance def: getLengthInitialValue() : String =
	let ba: AADLBA!BehaviorAnnex = self.getBehaviorAnnex() in
	let p: AADLI!FeatureInstance = self.getDirectlyUsedInFresh() in
	if(not p.oclIsUndefined()) then
		let package_name: String = p.getComponentInstance().subcomponent.classifier.getNamespace().getOwner().name in
		if(p.getComponentInstance().eContainer() = self.getComponentInstance().eContainer()) then
			package_name+thisModule.getPackageSuffix().concat('::'+p.feature.dataFeatureClassifier.getFreshnessType()+thisModule.impl_suffix).getProgrammationLanguageIdentifier()
		else
			package_name+thisModule.getPackageSuffix().concat('::'+self.feature.dataFeatureClassifier.getFreshnessType()+thisModule.impl_suffix).getProgrammationLanguageIdentifier()
		endif
	else
		if(self.isEventPort()) then
			'Integer_16'.asClassifier('BASE_TYPES').getProgrammationLanguageIdentifier()
		else 
			self.feature.dataFeatureClassifier.getProgrammationLanguageIdentifier()
		endif
	endif
;

helper context AADLI!FeatureInstance def: getBehaviorAnnex() : AADLBA!BehaviorAnnex =
	self.getComponentInstance().subcomponent.classifier.getBA()
;

helper context AADLI!SubprogramCall def: getSubprogramClassifier(): AADLBA!SubprogramClassifier = 
	if(self.calledSubprogram.oclIsTypeOf(AADLBA!SubprogramAccess))then
		self.calledSubprogram.subprogramFeatureClassifier
	else
		self.calledSubprogram
	endif
;

helper context AADLI!SubprogramClassifier def: needsContext() : Boolean =
	not self.getOrderedFeature()->select(e|e.oclIsKindOf(AADLI!Port))->isEmpty()
;

helper context AADLI!SubprogramClassifier def: getType() : AADLBA!SubprogramType =
	if(self.oclIsKindOf(AADLBA!SubprogramImplementation)) then
		self.type
	else
		self
	endif
;

helper context AADLI!ComponentInstance def: getOSMaxPriority() : Integer =
	let res: AADLBA!IntegerLiteral = self.ownedPropertyAssociation
		->any(pa|pa.property.name='Priority_Range') in 
	if(res.oclIsUndefined()) then
		10
	else
		res.ownedValue
		->first().ownedValue.maximum.value.longValue()
	endif
;

-- getThreadsInMode
helper context AADLI!ComponentInstance def: getThreadsInMode(m: AADLI!ModeInstance) : Sequence(AADLI!ComponentInstance) =
	self.componentInstance->select(e | e.category = #thread)
		->select(e|e.inMode->includes(m))
;
helper context AADLI!ComponentInstance def: getThreads() : Sequence(AADLI!ComponentInstance) =
	self.componentInstance->select(e | e.category = #thread)
;

helper context OclAny def:minus(i: OclAny) : OclAny =
	thisModule.theHOOK.minus(self,i)
;

helper context AADLI!InstanceObject def: getModeDataType(): AADLBA!DataClassifier = 
	AADLI!DataType->allInstances()->any(e|e.name = self.getProcess().name+'_modeDataClassifier')
;

helper context AADLI!ComponentInstance def: needsModeVariable() : Boolean =
	self.featureInstance->exists(f|f.needsModeVariable())
;

helper context AADLI!FeatureInstance def: needsModeVariable() : Boolean =
	AADLI!ModeInstance.allInstancesFrom('IN')->exists(mi|self.connectedInMode(mi.mode))
;

helper def : getStringLiteral(pa: AADLBA!PropertyAssociation, stringLiteralValue: String) : AADLI!PropertyValue =
	thisModule.theHOOK.getStringLiteral(pa, stringLiteralValue)
;

helper context AADLI!Classifier def: getEnumerators() : AADLI!PropertyValue =
	thisModule.theHOOK.getEnumerators(self)
;