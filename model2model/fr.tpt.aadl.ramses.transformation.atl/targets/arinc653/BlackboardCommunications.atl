--
-- AADL-RAMSES
-- 
-- Copyright Â© 2012 TELECOM ParisTech and CNRS
-- 
-- TELECOM ParisTech/LTCI
-- 
-- Authors: see AUTHORS
-- 
-- This program is free software: you can redistribute it and/or modify 
-- it under the terms of the Eclipse Public License as published by Eclipse,
-- either version 1.0 of the License, or (at your option) any later version.
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
-- Eclipse Public License for more details.
-- You should have received a copy of the Eclipse Public License
-- along with this program.  If not, see 
-- http://www.eclipse.org/org/documents/epl-v10.php
--

-- @nsURI AADLBA=/fr.tpt.aadl.annex.behavior/model/aadlba.ecore

module BlackboardCommunications;
create OUT : AADLBA 	from 		IN : AADLI,
									AADL_RUNTIME: AADLBA,
									DATA_MODEL: AADLBA,
									PROGRAMMING_PROPERTIES: AADLBA,
									HOOKS: ATLHOOKS,

									ARINC653_RUNTIME: AADLBA;


uses Services;
uses AADLCopyHelpers;
uses AADLICopyHelpers;
uses Uninstanciate;
uses PropertiesTools;

--------------------------------------------
---              BLACKBOARD              ---
--------------------------------------------

rule addReadBlackBoard(seq : AADLBA!SubprogramCallSequence,
					   fi: AADLI!FeatureInstance,
					   impl:  AADLI!ComponentImplementation, 
					   implImg: AADLBA!ComponentImplementation)
{
	do
	{
		thisModule.addLengthDataSubcomponent(fi, implImg);
		thisModule.addTimeOutSubcomponent(fi, implImg);	
		-- add call to Read_Blackboard
		thisModule.addReadBlackBoardCallSpecification
		(
			seq,
			'Read_Blackboard'.asSubprogramType('ARINC653_RUNTIME'),
			fi,
			implImg
		);
	}
}

rule addReadBlackBoardAction(actions : AADLBA!Actions,
					   fi: AADLI!FeatureInstance, 
					   implImg: AADLBA!ComponentImplementation)
{
	do
	{
		thisModule.addLengthDataSubcomponent(fi, implImg);
		thisModule.addTimeOutSubcomponent(fi, implImg);	
		-- add call to Read_Blackboard
		thisModule.addReadBlackBoardCallAction
		(
			actions,
			'Read_Blackboard'.asSubprogramType('ARINC653_RUNTIME'),
			fi,
			implImg
		);
		
	}
}

rule addDisplayBlackBoard(seq : AADLBA!SubprogramCallSequence,
						  fi: AADLI!FeatureInstance,
						  impl:  AADLI!ComponentImplementation, 
						  implImg: AADLBA!ComponentImplementation)
{
	do
	{
		thisModule.addLengthDataSubcomponent(fi,implImg);
		thisModule.addTimeOutSubcomponent(fi,implImg);
					
		-- add call to Display_Blackboard
		thisModule.addDisplayBlackBoardCallSpecification
		(
			seq,
			'Display_Blackboard'.asSubprogramType('ARINC653_RUNTIME'),
			fi,
			implImg
		);

	}
}

rule addDisplayBlackBoardAction(actions : AADLBA!Actions,
					   			fi: AADLI!FeatureInstance, 
					   			implImg: AADLBA!ComponentImplementation)
{
	do
	{
		thisModule.addLengthDataSubcomponent(fi,implImg);
		thisModule.addTimeOutSubcomponent(fi,implImg);
		-- add call to Read_Blackboard
		thisModule.addDisplayBlackBoardCallAction
		(
			actions,
			'Display_Blackboard'.asSubprogramType('ARINC653_RUNTIME'),
			fi,
			implImg
		);
		
	}
}

rule addDisplayBlackBoardCallSpecification(seq : AADLBA!SubprogramCallSequence,
										   spg: AADLBA!SubprogramType,
										   p: AADLI!FeatureInstance, 
										   implImg: AADLBA!ThreadImplementation)
{
	using
	{
		resolvedSpg: AADLBA!SubprogramType = thisModule.createResolvedSpg(p, spg);
	}
	to
		call: AADLBA!SubprogramCall
		(
			name<-thisModule.callPrefix()+spg.name+p.name,
			calledSubprogram<-resolvedSpg
		)
	do
	{
		thisModule.addBlackBoardCallSpecification(call, spg, p, implImg);
		seq.getOwnedCallSpecifications().add(call);
	}
}


rule addReadBlackBoardCallAction ( 	actions:AADLBA!Actions,
									spg: AADLBA!SubprogramType, 
								    p: AADLI!Port, 
									implImg: AADLBA!SubprogramImplementation)
{
	using
	{
		TIME_OUT: AADLBA!DataSubcomponent = p.retreiveTimeOutValue(implImg);
		BLACKBOARD_ID: AADLBA!DataAccess = p.getFeatureImg(); 
		MESSAGE_ADDR: AADLBA!DataSubcomponent = p.retreiveMessageAddress();
		LENGTH: AADLBA!DataSubcomponent = p.retreiveLengthValue	(implImg);
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.retreiveReturnCodeValue(implImg);
		resolvedSpg: AADLBA!SubprogramType = thisModule.createResolvedSpg(p, spg);
	}
	to
	
		call: AADLBA!SubprogramCallAction
		(
			subprogram <- callHolder,
			parameterLabels <- Sequence{BLACKBOARD_ID_HOLDER,TIMEOUT_HOLDER,MESSAGE_ADDR_HOLDER,LENGTH_HOLDER,RETURN_CODE_HOLDER} 
		),
		callHolder: AADLBA!CalledSubprogramHolder
		(
			element <- resolvedSpg
		),
		BLACKBOARD_ID_HOLDER: AADLBA!DataAccessHolder
		(
			element<-BLACKBOARD_ID
		),
		MESSAGE_ADDR_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- MESSAGE_ADDR
		),
		LENGTH_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- LENGTH
		),
		TIMEOUT_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- TIME_OUT
		),
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
	do
	{
		actions.add(call);
	}
}


rule addDisplayBlackBoardCallAction ( actions:AADLBA!Actions,
									  spg: AADLBA!SubprogramType, 
								      p: AADLI!Port, 
									  implImg: AADLBA!SubprogramImplementation)
{
	using
	{
		TIME_OUT: AADLBA!DataSubcomponent = p.retreiveTimeOutValue(implImg);
		BLACKBOARD_ID: AADLBA!DataAccess = p.getFeatureImg(); 
		MESSAGE_ADDR: AADLBA!DataSubcomponent = p.retreiveMessageAddress();
		LENGTH: AADLBA!DataSubcomponent = p.retreiveLengthValue	(implImg);
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.retreiveReturnCodeValue(implImg);
		resolvedSpg: AADLBA!SubprogramType = thisModule.createResolvedSpg(p, spg);
	}
	to
		call: AADLBA!SubprogramCallAction
		(
			subprogram <- callHolder,
			parameterLabels <- Sequence{BLACKBOARD_ID_HOLDER,MESSAGE_ADDR_HOLDER,LENGTH_HOLDER,RETURN_CODE_HOLDER} 
		),
		callHolder: AADLBA!CalledSubprogramHolder
		(
			element <- resolvedSpg
		),
		BLACKBOARD_ID_HOLDER: AADLBA!DataAccessHolder
		(
			element<-BLACKBOARD_ID
		),
		MESSAGE_ADDR_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- MESSAGE_ADDR
		),
		LENGTH_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- LENGTH
		),
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
	do
	{
		actions.add(call);
	}
}

rule addReadBlackBoardCallSpecification(seq : AADLBA!SubprogramCallSequence,
										spg: AADLBA!SubprogramType,
										p: AADLI!FeatureInstance,
										implImg: AADLBA!ThreadImplementation)
{
	using
	{
		TIME_OUT: AADLBA!DataSubcomponent = p.retreiveTimeOutValue(implImg);
		resolvedSpg: AADLBA!SubprogramType = thisModule.createResolvedSpg(p, spg);
	}
	to
		call: AADLBA!SubprogramCall
		(
			name<-thisModule.callPrefix()+spg.name+p.name,
			calledSubprogram<-resolvedSpg
		),
		connectedParam_TIME_OUT: AADLBA!ConnectedElement
		(
			connectionEnd<-spg.ownedParameter->first().debug('TimeOut Parameter Connection End')
		),
		connectedSubcomponent_TIME_OUT: AADLBA!ConnectedElement
		(
			connectionEnd<-TIME_OUT.debug('TimeOut Subcomponent Connection End')
		),
		paramConnection_TIME_OUT: AADLBA!ParameterConnection
		(
			name<-p.name+'_to_TimeOut',
			source<-connectedParam_TIME_OUT,
			destination<-connectedSubcomponent_TIME_OUT
		)
	do
	{
		thisModule.addBlackBoardCallSpecification(call, spg, p, implImg);
		connectedParam_TIME_OUT.setContext(call);
		implImg.getOwnedParameterConnections().add(paramConnection_TIME_OUT);
		seq.getOwnedCallSpecifications().add(call);
	}
}

rule addBlackBoardCallSpecification(call: AADLBA!SubprogramCall, spg: AADLBA!SubprogramType, p: AADLI!FeatureInstance, implImg: AADLBA!ThreadImplementation)
{
	using
	{
		BLACKBOARD_ID: AADLBA!DataAccess = p.getFeatureImg(); 
		MESSAGE_ADDR: AADLBA!DataSubcomponent = p.retreiveMessageAddress();
		LENGTH: AADLBA!DataSubcomponent = p.retreiveLengthValue	(implImg);
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.retreiveReturnCodeValue(implImg);
	}
	to
		
		connected_BLACKBOARD_ID_Access: AADLBA!ConnectedElement
		(
			connectionEnd<-spg.ownedDataAccess->first().debug('Id Data Access Connection End')
		),
		connected_BLACKBOARD_ID: AADLBA!ConnectedElement
		(
			connectionEnd<-BLACKBOARD_ID.debug('Id Subcomponent Connection End')
		),
		accessIDConnection: AADLBA!AccessConnection
		(
			name<-p.name+'_to_ID',
			accessCategory<-#data,
			source<-connected_BLACKBOARD_ID_Access,
			destination<-connected_BLACKBOARD_ID
		),
		
		connectedParam_MESSAGE_ADDR: AADLBA!ConnectedElement
		(
			connectionEnd<-spg.ownedParameter->any(e|e.name='MESSAGE_ADDR').debug('MessageAddr Parameter Connection End')
		),
		connectedSubcomponent_MESSAGE_ADDR: AADLBA!ConnectedElement
		(
			connectionEnd<-MESSAGE_ADDR.debug('MessageAddr Subcomponent Connection End')
		),
		paramConnection_MESSAGE_ADDR: AADLBA!ParameterConnection
		(
			name<-p.name+'_to_MessageAddr',
			source<-connectedParam_MESSAGE_ADDR,
			destination<-connectedSubcomponent_MESSAGE_ADDR
		),
		
		connectedParam_LENGTH: AADLBA!ConnectedElement
		(
			connectionEnd<-spg.ownedParameter->any(e|e.name='LENGTH').debug('Length Parameter Connection End')
		),
		connectedSubcomponent_LENGTH: AADLBA!ConnectedElement
		(
			connectionEnd<-LENGTH.debug('Length Subcomponent Connection End')
		),
		
		paramConnection_LENGTH: AADLBA!ParameterConnection
		(
			name<-p.name+'_to_MessageLength',
			source<-connectedParam_LENGTH,
			destination<-connectedSubcomponent_LENGTH
		),
		
		connectedParam_RETURN_CODE: AADLBA!ConnectedElement
		(
			connectionEnd<-spg.ownedParameter->any(e|e.name='RETURN_CODE').debug('Subprogram Parameter Connection End')
		),
		connectedSubcomponent_RETURN_CODE: AADLBA!ConnectedElement
		(
			connectionEnd<-RETURN_CODE.debug('Subprogram Subcomponent Connection End')
		),
		paramConnection_RETURN_CODE: AADLBA!ParameterConnection
		(
			name<-p.name+'_to_ReturnCode',
			source<-connectedParam_RETURN_CODE,
			destination<-connectedSubcomponent_RETURN_CODE
		)
		
	do
	{
		
		connectedParam_MESSAGE_ADDR.setContext(call);
		connected_BLACKBOARD_ID_Access.setContext(call);
		connectedParam_RETURN_CODE.setContext(call);
		connectedParam_LENGTH.setContext(call);
		
		implImg.getOwnedParameterConnections().add(paramConnection_MESSAGE_ADDR);
		implImg.getOwnedAccessConnections().add(accessIDConnection);
		implImg.getOwnedParameterConnections().add(paramConnection_RETURN_CODE);
		implImg.getOwnedParameterConnections().add(paramConnection_LENGTH);
		
		call.debug('Created Call Sequence');
		call;
	}
}
