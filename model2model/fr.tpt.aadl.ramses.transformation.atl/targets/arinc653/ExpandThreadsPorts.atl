--
-- AADL-RAMSES
-- 
-- Copyright Â© 2012 TELECOM ParisTech and CNRS
-- 
-- TELECOM ParisTech/LTCI
-- 
-- Authors: see AUTHORS
-- 
-- This program is free software: you can redistribute it and/or modify 
-- it under the terms of the Eclipse Public License as published by Eclipse,
-- either version 1.0 of the License, or (at your option) any later version.
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
-- Eclipse Public License for more details.
-- You should have received a copy of the Eclipse Public License
-- along with this program.  If not, see 
-- http://www.eclipse.org/org/documents/epl-v10.php
--

-- @nsURI AADLBA=/fr.tpt.aadl.annex.behavior/model/aadlba.ecore

module ExpandThreadsPorts;
create OUT : AADLBA 	from 		IN : AADLI,
									AADL_RUNTIME: AADLBA,
									DATA_MODEL: AADLBA,
									PROGRAMMING_PROPERTIES: AADLBA,
									HOOKS: ATLHOOKS,

									POK_RUNTIME: AADLBA;


uses Services;
uses AADLCopyHelpers;
uses AADLICopyHelpers;
uses Uninstanciate;
uses PropertiesTools;

-------------------------------------------------------------------------------------------
----		TO BE SUPERIMPOSED RULES
-------------------------------------------------------------------------------------------
	
rule expandThreadDispatchProtocol(inst: AADLBA!ComponentInstancs, implImg: AADLBA!ThreadImplementation) 
{
}
	

rule threadImplementationPostTreatment(implImg: AADLBA!BehavioredImplementation)
{
	do
	{
		thisModule.addReturnCodeDataSubcomponent(implImg);
	}
}

-------------------------------------------------------------------------------------------
----	OVERRIDEN HELPERS
-------------------------------------------------------------------------------------------


helper context AADLI!ConnectedElement def : getConnFeatureImg(owner : AADLBA!ComponentImplementation) : AADLBA!Feature =
	if(not self.connectionEnd.oclIsKindOf(AADLI!Feature)) then
		owner.ownedSubcomponent->any(f|f.name=self.connectionEnd.name)
	else
		if (self.context.oclIsUndefined()) then
			if(self.connectionEnd.oclIsKindOf(AADLI!Port)) then
				if(self.connectionEnd.isDataOrEventDataPort() and self.connectionEnd.isThreadPort()) then
					self.connectionEnd.retreiveIdAccess(owner).debug(owner.name+' Port Id access')
				else
					owner.getType().getOwnedFeatures()->any(f|f.name=self.connectionEnd.name)
				endif
			else
				owner.getType().getOwnedFeatures()->any(f|f.name=self.connectionEnd.name)
			endif
		else
			if (self.getConnContextImg(owner).oclIsUndefined()) then
			    -- subcomponent is not yet mapped: feature cannot be resolved yet
				OclUndefined
			else 
				if (self.getConnContextImg(owner).oclIsKindOf(AADLBA!SubprogramCall)) then
					self.getConnContextImg(owner).calledSubprogram.ownedFeature->any(p|p.name=self.connectionEnd.name)
				else
					-- subcomponent is mapped: feature is accessible
					self.getConnContextImg(owner).getType().getOwnedFeatures()->any(f|f.name=self.connectionEnd.name)
				endif
			endif
		endif
	endif
;




rule PortConnection_Instance
{
	from
		cnxInst: AADLI!ConnectionInstance
		(
			thisModule.toBeTransformedInterProcessConnection(cnxInst)
		)
		
	using
	{
		c_source: AADLBA!ComponentInstance = cnxInst.source.eContainer();
		c_destination: AADLBA!ComponentInstance = cnxInst.destination.eContainer();
		process_sourceImplImg: AADLBA!ProcessImplementation = OclUndefined;
		process_destinationImplImg: AADLBA!ProcessImplementation = OclUndefined;
		sourceImplImg: AADLBA!ThreadImplementation = OclUndefined;
		sourceImpl: AADLI!ComponentImplementation = c_source.subcomponent.subcomponentType;
		destinationImplImg: AADLBA!ThreadImplementation = OclUndefined;
		destinationImpl: AADLI!ComponentImplementation = c_destination.subcomponent.subcomponentType;
		sourceFeatureImg: AADLBA!DataAccess = OclUndefined;
		destinationFeatureImg: AADLBA!DataAccess = OclUndefined;

	}
	to
		
		-- creation of a data subcomponent, to be added to the container process of target thread component, 
		-- this data is a global variable representing the
		-- ARINC653 communication identifier.
		communicationSrcID: AADLBA!DataSubcomponent
			(name<-'src_'+cnxInst.source.name+thisModule.dataSubcomponentSuffix()),
		communicationDstID: AADLBA!DataSubcomponent
			(name<-'dst_'+cnxInst.source.name+thisModule.dataSubcomponentSuffix())
	do
	{
		cnxInst.debug('initializing connection instance');
		
		ATLHOOKS!HookAccess.allInstancesFrom('HOOKS')->first().addTransformationBackTrace(communicationSrcID, cnxInst.source);
		ATLHOOKS!HookAccess.allInstancesFrom('HOOKS')->first().addTransformationBackTrace(communicationDstID, cnxInst.destination);
		
		sourceImplImg <- c_source.getSubcomponentImg().subcomponentType.debug('Retreived Component Implementation');
		destinationImplImg <- c_destination.getSubcomponentImg().subcomponentType.debug('Retreived Component Implementation');
		process_sourceImplImg <- c_source.eContainer().getSubcomponentImg().subcomponentType.debug('Retreived Process Implementation');
		process_destinationImplImg <- c_destination.eContainer().getSubcomponentImg().subcomponentType.debug('Retreived Process Implementation');
		sourceFeatureImg <- cnxInst.source.getFeatureImg().debug('Retreived Feature Image');
		destinationFeatureImg <- cnxInst.destination.getFeatureImg().debug('Retreived Feature Image');
		if(process_destinationImplImg.ownedDataSubcomponent->any(e|e.name=communicationDstID.name).oclIsUndefined())
			process_destinationImplImg.ownedDataSubcomponent.add(communicationDstID);
		if(process_sourceImplImg.ownedDataSubcomponent->any(e|e.name=communicationSrcID.name).oclIsUndefined())
			process_sourceImplImg.ownedDataSubcomponent.add(communicationSrcID);
		
		if(cnxInst.source.category=#eventPort or cnxInst.source.category=#eventDataPort)
		{
			sourceFeatureImg.dataFeatureClassifier <- 'Queuing_Port_Id_Type'.asDataType('POK_RUNTIME');
			destinationFeatureImg.dataFeatureClassifier <- 'Queuing_Port_Id_Type'.asDataType('POK_RUNTIME');
			thisModule.addQueuingGlobalVariable(cnxInst.source, communicationSrcID, process_sourceImplImg, sourceImplImg);
			thisModule.addQueuingGlobalVariable(cnxInst.destination, communicationDstID, process_destinationImplImg, destinationImplImg);
		}
		else if(cnxInst.source.category=#dataPort)
		{
			sourceFeatureImg.dataFeatureClassifier<-'Sampling_Port_Id_Type'.asDataType('POK_RUNTIME');
			destinationFeatureImg.dataFeatureClassifier<-'Sampling_Port_Id_Type'.asDataType('POK_RUNTIME');
			thisModule.addSamplingGlobalVariable(cnxInst.source, communicationSrcID, process_sourceImplImg, sourceImplImg);
			thisModule.addSamplingGlobalVariable(cnxInst.destination, communicationDstID, process_destinationImplImg, destinationImplImg);
		}
		
		thisModule.addThreadsInternalConnections(cnxInst.source);
		thisModule.addThreadsInternalConnections(cnxInst.destination);
		thisModule.setSourcePortAccessorParameterConnections(cnxInst.source, sourceImpl, sourceImplImg);
		if(c_source.featureInstance->last().debug('Last Feature Instance') = cnxInst.source.debug('Current Feature Instance'))
		{
			true.debug('Expand Dispatch Protocol');
			thisModule.expandThreadDispatchProtocol(c_source, sourceImplImg);
		}
		thisModule.setDestinationPortAccessorParameterConnections(cnxInst.destination, destinationImpl, destinationImplImg);
		if(c_destination.featureInstance->last().debug('Last Feature Instance') = cnxInst.destination.debug('Current Feature Instance'))
		{
			true.debug('Expand Dispatch Protocol');
			thisModule.expandThreadDispatchProtocol(c_destination, destinationImplImg);
		}
		
		thisModule.addImportedUnit(thisModule.public(), 'POK_RUNTIME', 'pok_runtime');
		thisModule.addImportedUnit(thisModule.public(), 'BASE_TYPES', 'Base_Types');
	}
}

-------------------------------------------------------------------------------------------
----		ADDED MATCHED RULES
-------------------------------------------------------------------------------------------

rule intraProcess_PortConnection_Instance
{
	from
		cnxInst: AADLI!ConnectionInstance
		(
			thisModule.toBeTransformedIntraProcessConnection(cnxInst)
		)
	using
	{
		c_source: AADLBA!ComponentInstance = cnxInst.source.eContainer();
		c_destination: AADLBA!ComponentInstance = cnxInst.destination.eContainer();
		impl_source: AADLBA!ComponentClassifier = c_source.subcomponent.subcomponentType;
		impl_destination: AADLBA!ComponentClassifier = c_destination.subcomponent.subcomponentType;
		processImplImg: AADLBA!ProcessImplementation = OclUndefined;
		sourceImplImg: AADLBA!ThreadImplementation = OclUndefined;
		destinationImplImg: AADLBA!ThreadImplementation = OclUndefined;
		sourceFeatureImg: AADLBA!Feature = OclUndefined;
		destinationFeatureImg: AADLBA!Feature = OclUndefined;
	}
	to
		d: AADLBA!DataSubcomponent
			(name<-cnxInst.destination.name+thisModule.dataSubcomponentSuffix())
	do
	{
		ATLHOOKS!HookAccess.allInstancesFrom('HOOKS')->first().addTransformationBackTrace(d, cnxInst.destination);
		
		sourceImplImg <- c_source.getSubcomponentImg().subcomponentType.debug('Retreived Component Implementation');
		destinationImplImg <- c_destination.getSubcomponentImg().subcomponentType.debug('Retreived Component Implementation');
		processImplImg <- c_source.eContainer().getSubcomponentImg().subcomponentType.debug('Retreived Process Implementation');
		sourceFeatureImg <- cnxInst.source.getFeatureImg().debug('Retreived Feature Image');
		destinationFeatureImg <- cnxInst.destination.getFeatureImg().debug('Retreived Feature Image'); 
		if(processImplImg.ownedDataSubcomponent->any(e|e.name=d.name).oclIsUndefined())
			processImplImg.ownedDataSubcomponent.add(d);
		
		-- add and connect data subcomponent, Id of the communication protocol
		if(cnxInst.source.category=#eventPort)
		{
			d.dataSubcomponentType<-'Event_Id_Type'.asDataType('POK_RUNTIME');
			sourceFeatureImg.dataFeatureClassifier <- 'Event_Id_Type'.asDataType('POK_RUNTIME');
			destinationFeatureImg.dataFeatureClassifier <- 'Event_Id_Type'.asDataType('POK_RUNTIME');
		}
		else if(cnxInst.source.category=#eventDataPort)
		{
			d.dataSubcomponentType<-'Buffer_Id_Type'.asDataType('POK_RUNTIME');
			sourceFeatureImg.dataFeatureClassifier <- 'Buffer_Id_Type'.asDataType('POK_RUNTIME');
			destinationFeatureImg.dataFeatureClassifier <- 'Buffer_Id_Type'.asDataType('POK_RUNTIME');
		}
		else if(cnxInst.source.category=#dataPort)
		{
			-- add data subcomponents for Blackboard
			destinationFeatureImg.dataFeatureClassifier <- 'Blackboard_Id_Type'.asDataType('POK_RUNTIME');
			sourceFeatureImg.dataFeatureClassifier <- 'Blackboard_Id_Type'.asDataType('POK_RUNTIME');
			d.dataSubcomponentType<-'Blackboard_Id_Type'.asDataType('POK_RUNTIME');
		}
		
		thisModule.addDataAccessConnection(
			processImplImg,
			'Id_instance_to_'+cnxInst.source.eContainer().name+'_'+cnxInst.source.name,
			cnxInst.source.retreiveIdAccess(sourceImplImg), 
			c_source.getSubcomponentImg(),
			d
		);
		
		thisModule.addDataAccessConnection(
			processImplImg,
			'Id_instance_to_'+cnxInst.destination.eContainer().name+'_'+cnxInst.destination.name,
			cnxInst.destination.retreiveIdAccess(destinationImplImg), 
			c_destination.getSubcomponentImg(),
			d
		);
		
		
		thisModule.addThreadsInternalConnections(cnxInst.destination);
		thisModule.addThreadsInternalConnections(cnxInst.source);
		thisModule.setSourcePortAccessorParameterConnections(cnxInst.source, impl_source, sourceImplImg);
		if(c_source.featureInstance->last().debug('Last Feature Instance') = cnxInst.source.debug('Current Feature Instance'))
		{
			true.debug('Expand Dispatch Protocol');
			thisModule.expandThreadDispatchProtocol(c_source, sourceImplImg);
			
		}
		thisModule.setDestinationPortAccessorParameterConnections(cnxInst.destination, impl_destination, destinationImplImg);
		if(c_destination.featureInstance->last().debug('Last Feature Instance') = cnxInst.destination.debug('Current Feature Instance'))
		{
			true.debug('Expand Dispatch Protocol');
			thisModule.expandThreadDispatchProtocol(c_destination, destinationImplImg);
			
		}
		thisModule.addImportedUnit(thisModule.public(), 'POK_RUNTIME', 'pok_runtime');
		thisModule.addImportedUnit(thisModule.public(), 'BASE_TYPES', 'Base_Types');
	}
}

-------------------------------------------------------------------------------------------
----		ADDED HELPERS
-------------------------------------------------------------------------------------------

helper def: toBeTransformedInterProcessConnection(cnxInst: AADLI!ConnectionInstance): Boolean =
	(cnxInst.kind = #portConnection and
	cnxInst.source.eContainer().category=#thread) and
	not cnxInst.isIntraProcessConnection()
	and not cnxInst.source.isPeriodicDelayedPort()
;

helper def: toBeTransformedIntraProcessConnection(cnxInst: AADLI!ConnectionInstance): Boolean =
	(cnxInst.kind = #portConnection and
	cnxInst.source.eContainer().category=#thread and
	cnxInst.isIntraProcessConnection())
	and not cnxInst.source.isPeriodicDelayedPort()
;

helper def : getTimeOutInitValue(port : AADLBA!Feature) : AADLBA!PropertyExpression =
	if(port.getModalPropertyValue('Timeout').oclIsUndefined())
	then
		OclUndefined
	else
		if(port.getModalPropertyValue('Timeout').ownedValue.value.oclIsUndefined())
		then
			OclUndefined
		else
			thisModule.toStringLiteral(port.getModalPropertyValue('Timeout').ownedValue.value.toString())
		endif
	endif
;

helper context AADLI!FeatureInstance def : retreiveMessageAddress() : AADLBA!DataSubcomponent =
	thisModule.resolveTemp(self, 'dataSubcomponent')
;

helper context AADLI!FeatureInstance def : retreiveLengthValue(threadImplImg: AADLBA!ThreadImplementation) : AADLBA!DataSubcomponent =
	threadImplImg.ownedDataSubcomponent->any(e| e.name=self.name+'_Length')
;

helper def : retreiveReturnCodeValue(threadImplImg: AADLBA!ThreadImplementation) : AADLBA!DataSubcomponent =
	if(threadImplImg.ownedDataSubcomponent->any(e| e.name='runtime_call_ret').oclIsUndefined() )
	then
		thisModule.addReturnCodeDataSubcomponent(threadImplImg)
	else
		threadImplImg.ownedDataSubcomponent->any(e| e.name='runtime_call_ret')
	endif
;

helper context AADLI!FeatureInstance def : retreiveTimeOutValue(threadImplImg: AADLBA!ThreadImplementation) : AADLBA!DataSubcomponent =
	threadImplImg.ownedDataSubcomponent->any(e| e.name=self.name+'_TimeOut')
;

helper context AADLI!FeatureInstance def : retreiveValidityValue(threadImplImg: AADLBA!ThreadImplementation) : AADLBA!DataSubcomponent =
	threadImplImg.ownedDataSubcomponent->any(e| e.name=self.name+'_Validity')
;

helper context AADLI!FeatureInstance def : retreiveIdValue(processImplImg: AADLBA!ProcessImplementation) : AADLBA!DataAccess =
	processImplImg.ownedDataSubcomponent->any(e| e.name=self.name+thisModule.dataSubcomponentSuffix())
;

helper context AADLI!ConnectionInstanceEnd def : getDestinations() : Sequence(AADLI!ConnectionInstanceEnd) =
    AADLI!ConnectionInstance->allInstancesFrom('IN')->select(c|c.source=self)
                            ->collect(c|c.destination)
;

helper context AADLI!ConnectionInstanceEnd def : getSources() : Sequence(AADLI!ConnectionInstanceEnd) =
    AADLI!ConnectionInstance->allInstancesFrom('IN')->select(c|c.destination=self)
                            ->collect(c|c.source)
; 

helper context AADLI!ComponentInstance def : getProcessorBinding() : AADLI!SubcomponentImplementation =
	self.ownedPropertyAssociation->any(e | e.property = AADLBA!Property->allInstancesFrom('DEPLOYMENT_PROPERTIES')->any(f|f.name = 'Actual_Processor_Binding')).appliesTo
;


-------------------------------------------------------------------------------------------
----		ADDED LAZY RULES
-------------------------------------------------------------------------------------------

rule setDestinationPortAccessorParameterConnections(port: AADLI!FeatureInstance,
													impl: AADLI!ComponentImplementation,
													implImg: AADLI!ComponentImplementation)
{
	do
	{
		for(cnx in impl.getOwnedParameterConnections())
		{
			if(cnx.source.connectionEnd = port.feature)
			{
				thisModule.addParameterConnection
				(
					implImg,
					'transfer_'+port.retreiveMessageAddress().name+'_to_'+cnx.destination.getConnContextImg(implImg).name+'_'+cnx.destination.getConnFeatureImg(implImg).name,
					cnx.destination.getConnFeatureImg(implImg),
					cnx.destination.getConnContextImg(implImg),
					port.retreiveMessageAddress()
				);
			}
		}
	}
}

rule setSourcePortAccessorParameterConnections(port: AADLI!FeatureInstance, 
											   impl: AADLI!ComponentImplementation, 
											   implImg: AADLI!ComponentImplementation)
{
	do
	{
		for(cnx in impl.getOwnedParameterConnections())
		{
			if(cnx.destination.connectionEnd = port.feature)
			{
				thisModule.addParameterConnection
				(
					implImg,
					'transfer_'+
						port.retreiveMessageAddress().name+
						'_to_'+cnx.source.getConnContextImg(implImg).name+'_'+
						cnx.source.getConnFeatureImg(implImg).name,
					cnx.source.getConnFeatureImg(implImg),
					cnx.source.getConnContextImg(implImg),
					port.retreiveMessageAddress()
				);
			}
		}
	}
}

helper context AADLI!FeatureInstance def: getLengthInitialValue() : String =
	let ba: AADLBA!BehaviorAnnex = self.getBehaviorAnnex() in
	let p: AADLI!FeatureInstance = self.getDirectlyUsedInFresh() in 
	if(not p.oclIsUndefined()) then
		if(p.eContainer().eContainer() = self.eContainer().eContainer()) then
			thisModule.public().name.concat('::'+p.getFreshnessType()+thisModule.impl_suffix).get_C_Identifier()
		else
			thisModule.public().name.concat('::'+self.getFreshnessType()+thisModule.impl_suffix).get_C_Identifier()
		endif
	else
		self.feature.dataFeatureClassifier.get_C_Identifier()
	endif
;

unique lazy rule addLengthDataSubcomponent
{
	from
		port : AADLI!FeatureInstance,
		implImg : AADLBA!ThreadImplementation
	using
	{
		typeId: String = port.getLengthInitialValue();
		initValuePE: AADLBA!PropertyExpression = thisModule.CreateStringLiteralPropertyExpression('sizeof('+typeId+')');
	}
	to
		lv: AADLBA!ListValue
		(
			ownedListElement <- Sequence{initValuePE}
		),
		dataSubcomponent: AADLBA!DataSubcomponent
		(
			name<-port.feature.name+'_Length',
			dataSubcomponentType<-'Message_Size_Type'.asDataType('POK_RUNTIME'),
			ownedPropertyAssociation	<- Sequence {thisModule.CreatePropertyAssociation('Initial_Value','DATA_MODEL',lv)}
		)
	do
	{
		ATLHOOKS!HookAccess.allInstancesFrom('HOOKS')->first().addTransformationBackTrace(dataSubcomponent, port);
		implImg.ownedDataSubcomponent.add(dataSubcomponent);
		dataSubcomponent;
	}
}

unique lazy rule addValidityDataSubcomponent
{
	from
		port : AADLBA!FeatureInstance,
		implImg : AADLBA!ThreadImplementation
	to
		dataSubcomponent: AADLBA!DataSubcomponent
		(
			name<-port.feature.name+'_Validity',
			dataSubcomponentType<-'Validity_Type'.asDataType('POK_RUNTIME')
		)
	do
	{
		ATLHOOKS!HookAccess.allInstancesFrom('HOOKS')->first().addTransformationBackTrace(dataSubcomponent, port);
		implImg.ownedDataSubcomponent.add(dataSubcomponent);
		dataSubcomponent;
	}
}

unique lazy rule addTimeOutSubcomponent
{
	from
		port : AADLI!FeatureInstance,
		implImg : AADLBA!ThreadImplementation
		
	using
	{
		initValuePE: AADLBA!PropertyExpression = thisModule.getTimeOutInitValue(port.feature);
	}
	to
		dataSubcomponent: AADLBA!DataSubcomponent
		(
			name<-port.feature.name+'_TimeOut',
			dataSubcomponentType<-'System_Time_Type'.asDataType('POK_RUNTIME')
		)
	do
	{
		ATLHOOKS!HookAccess.allInstancesFrom('HOOKS')->first().addTransformationBackTrace(dataSubcomponent, port);
		implImg.ownedDataSubcomponent.add(dataSubcomponent);
		if(not initValuePE.oclIsUndefined())
		{
			dataSubcomponent.ownedPropertyAssociation.add(thisModule.CreatePropertyAssociation('Initial_Value','DATA_MODEL',thisModule.createListValueFromPropertyExpression(initValuePE)));
		}
		dataSubcomponent;
	}
}

unique lazy rule addReturnCodeDataSubcomponent
{
	from
		impl: AADLBA!ThreadImplementation
	to
		dataSubcomponent: AADLBA!DataSubcomponent
		(
			name <- 'runtime_call_ret',
			dataSubcomponentType<-'Return_Code_Type'.asDataType('POK_RUNTIME')
		)
	do
	{
		impl.getOwnedDataSubcomponents().add(dataSubcomponent);
		dataSubcomponent;
	}
}

------------------------------------
---      ADDED CALLED RULES     ----
------------------------------------

rule addNextValueSubprogramCallAction(actions:AADLBA!Actions,
								fi: AADLI!FeatureInstance,
								implImg: AADLBA!ComponentImplementation)
{
	do
	{
		for(cnxInst in fi.dstConnectionInstance)
		{
			if(thisModule.toBeTransformedInterProcessConnection(cnxInst))
			{
				if(cnxInst.source.category=#eventPort or cnxInst.source.category=#eventDataPort)
				{
					thisModule.addReceiveQueuingAction(actions, fi, implImg);
				}
				else if(cnxInst.source.category=#dataPort)
				{
					thisModule.addReadSamplingAction(actions, fi, implImg);
				}
			}
			else if(thisModule.toBeTransformedIntraProcessConnection(cnxInst))
			{
				if(cnxInst.source.category=#eventPort)
				{
					thisModule.addWaitEventAction(actions, fi, implImg);
				}
				else if(cnxInst.source.category=#eventDataPort)
				{
					thisModule.addReceiveBufferAction(actions, fi, implImg);
				}
				else if(cnxInst.source.category=#dataPort)
				{
					thisModule.addReadBlackBoardAction(actions, fi, implImg);
				}
			}
		}
	}
}

rule addNextValueSubprogramCall(seq : AADLBA!SubprogramCallSequence,
								impl : AADLBA!ComponentImplementation,
								implImg : AADLBA!ComponentImplementation,
								fi: AADLI!FeatureInstance)
{
	do
	{
		for(cnxInst in fi.dstConnectionInstance)
		{
			if(thisModule.toBeTransformedInterProcessConnection(cnxInst))
			{
				if(cnxInst.source.category=#eventPort or cnxInst.source.category=#eventDataPort)
				{
					thisModule.addReceiveQueuing(seq, fi, impl, implImg);
				}
				else if(cnxInst.source.category=#dataPort)
				{
					thisModule.addReadSampling(seq, fi, impl, implImg);
				}
			}
			else if(thisModule.toBeTransformedIntraProcessConnection(cnxInst))
			{
				if(cnxInst.source.category=#eventPort)
				{
					thisModule.addWaitEvent(seq, fi, impl, implImg);
				}
				else if(cnxInst.source.category=#eventDataPort)
				{
					thisModule.addReceiveBuffer(seq, fi, impl, implImg);
				}
				else if(cnxInst.source.category=#dataPort)
				{
					thisModule.addReadBlackBoard(seq, fi, impl, implImg);
				}
			}
		}
	}
}

rule addPutValueSubprogramCallAction(actions:AADLBA!Actions,
								fi: AADLI!FeatureInstance,
								implImg: AADLBA!ComponentImplementation)
{
	do
	{
		for(cnxInst in fi.srcConnectionInstance)
		{
			if(thisModule.toBeTransformedInterProcessConnection(cnxInst))
			{
				if(cnxInst.source.category=#eventPort or cnxInst.source.category=#eventDataPort)
				{
					thisModule.addSendQueuingAction(actions, fi, implImg);
				}
				else if(cnxInst.source.category=#dataPort)
				{
					thisModule.addWriteSamplingAction(actions, fi, implImg);
				}
			}
			else if(thisModule.toBeTransformedIntraProcessConnection(cnxInst))
			{
				if(cnxInst.source.category=#eventPort)
				{
					thisModule.addSetEventAction(actions, fi, implImg);
				}
				else if(cnxInst.source.category=#eventDataPort)
				{
					thisModule.addSendBufferAction(actions, fi, implImg);
				}
				else if(cnxInst.source.category=#dataPort)
				{
					thisModule.addDisplayBlackBoardAction(actions, fi, implImg);
				}
			}
		}
	}
}

rule addPutValueSubprogramCall(seq : AADLBA!SubprogramCallSequence,
								impl : AADLBA!ComponentImplementation,
								implImg : AADLBA!ComponentImplementation,
								fi: AADLI!FeatureInstance)
{
	do
	{
		for(cnxInst in fi.srcConnectionInstance)
		{
		
			if(thisModule.toBeTransformedInterProcessConnection(cnxInst))
			{
				if(cnxInst.destination.category=#eventPort or cnxInst.source.category=#eventDataPort)
				{
					thisModule.addSendQueuing(seq, fi, impl, implImg);
				}
				else if(cnxInst.destination.category=#dataPort)
				{
					thisModule.addWriteSampling(seq, fi, impl, implImg);
				}
			}
			else if(thisModule.toBeTransformedIntraProcessConnection(cnxInst))
			{
				if(cnxInst.destination.category=#eventPort)
				{
					thisModule.addSetEvent(seq, fi, impl, implImg);
				}
				else if(cnxInst.destination.category=#dataPort)
				{
					thisModule.addDisplayBlackBoard(seq, fi, impl, implImg);
				}
				else if(cnxInst.destination.category=#eventDataPort)
				{
					thisModule.addSendBuffer(seq, fi, impl, implImg);
				}
			}
		}
	}
}

rule addThreadsInternalConnections(fi: AADLI!FeatureInstance)
{
	using
	{
		c : AADLI!ComponentInstance = fi.eContainer();
		implImg: AADLBA!ComponentImplementation = c.getSubcomponentImg().subcomponentType;
		impl: AADLBA!ComponentImplementation = c.getImpl();
	}
	do
	{
		if(c.category=#thread and c.featureInstance->last() = fi)
		{
			for(cnx in impl.ownedParameterConnection)
			{
				if(cnx.source.connectionEnd.oclIsKindOf(AADLI!Parameter) 
						and cnx.destination.connectionEnd.oclIsKindOf(AADLI!Parameter))
				{
					thisModule.addSuprogramCallParameterConnection(implImg, cnx);
				}
			}
			implImg.ownedAccessConnection <- impl.ownedAccessConnection
					->select(co| 	not co.source.getConnFeatureImg(implImg).oclIsUndefined() and
									not co.destination.getConnFeatureImg(implImg).oclIsUndefined())
					->collect(co|thisModule.AccessConnection(co, implImg));
		}
	}
}

unique lazy rule createResolvedSpg
{
	from
		p: AADLI!Port,
		spg: AADLBA!SubprogramType
	using
	{
		param: AADLBA!Parameter = spg.ownedParameter->any(e|e.name = 'MESSAGE_ADDR');
		actualParam : AADLBA!Parameter = thisModule.resolveTemp(p, 'actualParam');
	}
	to
		spg_ResolvedType: AADLBA!SubprogramType
		(
			name <- spg.name+'_'+p.name,
			ownedExtension <- extendedMessageType,
			ownedParameter <- Sequence{actualParam}
		),
		extendedMessageType: AADLBA!TypeExtension
		(
			extended<-spg
		)
	do
	{
		actualParam.refined <- param;
		actualParam.direction <- param.direction;
		if(thisModule.public().ownedClassifier->any(e|e.name = spg_ResolvedType.name).oclIsUndefined())
			thisModule.public().ownedClassifier.add(spg_ResolvedType);
		spg_ResolvedType;
	}
}
