--
-- AADL-RAMSES
-- 
-- Copyright Â© 2012 TELECOM ParisTech and CNRS
-- 
-- TELECOM ParisTech/LTCI
-- 
-- Authors: see AUTHORS
-- 
-- This program is free software: you can redistribute it and/or modify 
-- it under the terms of the Eclipse Public License as published by Eclipse,
-- either version 1.0 of the License, or (at your option) any later version.
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
-- Eclipse Public License for more details.
-- You should have received a copy of the Eclipse Public License
-- along with this program.  If not, see 
-- http://www.eclipse.org/org/documents/epl-v10.php
--

-- @nsURI AADLBA=/fr.tpt.aadl.annex.behavior/model/aadlba.ecore
-- @atlcompiler emftvm

module ArincProperties;
create OUT : CV 	from 		IN : AADLI;

-- @extends check_ComponentInstance
abstract rule check_ProcessorInstance
{
	from
	  obj: AADLI!ComponentInstance
	  (obj.category = #processor)
}

-- @extends check_ProcessInstance
rule check_ArincProcessorBinding
{
	from
		obj: AADLI!ComponentInstance
		(obj.hasProperty('Actual_Processor_Binding')
			and
		 not (obj.getProcessorBinding()->first().category.getName() = 'virtual processor')
		)
	to
		err: CV!Error
		(message <- 'In ARINC653, process subcomponent '+obj.subcomponent.name + ' must be deployed on a virtual processor' )
}

-- @extends check_ProcessInstance
rule check_ArincUniqueProcessorBinding
{
	from
		obj: AADLI!ComponentInstance
		(
			obj.hasProperty('Actual_Processor_Binding')
			and
		 	obj.getProcessorBinding()->first().category.getName() = 'virtual processor'
		 	and
		 	obj.isNotAlone()
		)
	to
		err: CV!Error
		(message <- 'In ARINC653, each process subcomponent must'+ 
			'be deployed on a different virtual processor' )
}


--@extends check_ProcessorInstance
rule check_HasMajorFrame
{
	from
	 	obj:AADLI!ComponentInstance
		(not obj.hasProperty('Module_Major_Frame'))
	to
		err: CV!Error
		(message <- 'In ARINC653, each processor must'+ 
			'have a property value for ARINC653::Module_Major_Frame' )
}

--@extends check_ProcessorInstance
rule check_HasSlotsAllocation
{
	from
	 	obj:AADLI!ComponentInstance,
		sa: AADLI!Property
		(
			sa.name='Slots_Allocation'
			and
			not obj.hasProperty('Slots_Allocation')
		)
	to
		err: CV!Error
		(message <- 'In ARINC653, each processor must'+ 
			' have a property value for ARINC653::Slots_Allocation' )
}

--@extends check_ProcessorInstance
rule check_HasPartitionSlots
{
	from
	 	obj:AADLI!ComponentInstance,
		sa: AADLI!Property
		(
			sa.name='Slots_Allocation'
			and not obj.hasProperty('Partition_Slots')
		)
	to
		err: CV!Error
		(message <- 'In ARINC653, each processor must '+ 
			'have a property value for ARINC653::Partition_Slots' )
}

--@extends check_ProcessorInstance
rule check_ParitionSlotsAllocationNumber
{
	from
	 	obj:AADLI!ComponentInstance,
		ps: AADLI!PropertyAssociation,
		pa: AADLI!PropertyAssociation 
		( 
		  ps.eContainer() = obj
		  and
		  ps.property.name = 'Partition_Slots'
		  and
		  pa.property.name = 'Slots_Allocation'
		  and
		  not(obj.ownedPropertyAssociation->any(pa|pa.property.name='Partition_Slots').ownedValue->first().ownedValue.ownedListElement->size()
			=
		  obj.ownedPropertyAssociation->any(pa|pa.property.name='Slots_Allocation').ownedValue->first().ownedValue.ownedListElement->size())
		)
	to
		err: CV!Error
		(message <- 'ARINC653::Partition_Slots and ARINC653::Slots_Allocation'+
			' propeties and must contain the same number of elements')
}

--@extends check_ProcessorInstance
rule check_EachPartitionHasSlot
{
	from
	 	obj:AADLI!ComponentInstance,
		part:AADLI!ComponentInstance
		(
		  part.category.getName()='virtual processor'
		  and
		  obj.hasProperty('Slots_Allocation')
		  and
		  obj.componentInstance->contains(part)
		  and
		  not obj.ownedPropertyAssociation->any(pa|pa.property.name='Slots_Allocation').ownedValue->first().ownedValue.ownedListElement->exists(e|e.containmentPathElement->first().namedElement = part.subcomponent)
		)
	to
		err: CV!Error
		(message <- 'ARINC653::Slots_Allocation property must reference all the partitions; '+part.name+
			' is not referenced in the ARINC653::Slots_Allocation property')
}

--@extends check_VirtualProcessorInstance
rule check_HasPeriod
{
	from
	 	obj:AADLI!ComponentInstance,
		p: AADLI!Property
		(p.name='Period' and not obj.hasProperty('Period'))
	to
		err: CV!Error
		(message <- 'In ARINC653, each virtual processor must'+ 
			'have a property value for ARINC653::Period' )
}

--@extends check_ProcessorInstance
rule check_PortsFlushTime
{
	from
	 	obj:AADLI!ComponentInstance,
		pft: AADLI!PropertyAssociation
		(
			pft.eContainer() = obj
			and
			obj.hasProperty('PortsFlushTime')
			and
			obj.ownedPropertyAssociation->any(e|e.property.name = 'Ports_Flush_Time').ownedValue->first()
			.ownedValue.namedValue.name = 'Minor_Frame_Switch'
			and not obj.hasProperty('Module_Minor_Frame'))
	to
		err: CV!Error
		(message <- 'ARINC653: when ports are flushed at minor frames switch, ARINC653::Module_Minor_Frame must be set' )
}


helper context AADLI!ComponentInstance def: isNotAlone() : Boolean =
	thisModule.getAllInstances('process')->excluding(self)->collect(e| e.getProcessorBinding())
	->contains(e|e=self.getProcessorBinding())
;

