--
-- AADL-RAMSES
-- 
-- Copyright Â© 2012 TELECOM ParisTech and CNRS
-- 
-- TELECOM ParisTech/LTCI
-- 
-- Authors: see AUTHORS
-- 
-- This program is free software: you can redistribute it and/or modify 
-- it under the terms of the Eclipse Public License as published by Eclipse,
-- either version 1.0 of the License, or (at your option) any later version.
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
-- Eclipse Public License for more details.
-- You should have received a copy of the Eclipse Public License
-- along with this program.  If not, see 
-- http://www.eclipse.org/org/documents/epl-v10.php
--

-- @nsURI AADLBA=/fr.tpt.aadl.annex.behavior/model/aadlba.ecore
-- @atlcompiler emftvm

module BlackboardCommunications;
create OUT : AADLBA 	from 		IN : AADLI,
									AADL_RUNTIME: AADLBA,
									DATA_MODEL: AADLBA,
									PROGRAMMING_PROPERTIES: AADLBA,
									HOOKS: ATLHOOKS,

									ARINC653_RUNTIME: AADLBA;


--------------------------------------------
---              BLACKBOARD              ---
--------------------------------------------

-- @extends m_Not_PeriodicDelayed_Port
abstract rule m_BlackBoardCallSpecification
{
	from
		fi: AADLI!FeatureInstance,
		callSequence: AADLI!SubprogramCallSequence
		(
			fi.isDataPort()
			and
			callSequence.isComputeEntryPointOf(fi.eContainer())
			and
			fi.isIntraProcessFeatureInstance()
		)
	to
		call: AADLBA!SubprogramCallAction
}

	
-- @extends m_BlackBoardCallSpecification,m_Input_DataPort_ComputeEntrypointCallSequence
rule m_Input_BlackBoard_ComputeEntrypointCallSequence
{
	from
		fi: AADLI!FeatureInstance,
		callSequence: AADLI!SubprogramCallSequence
	using
	{
		MESSAGE_ADDR: AADLBA!DataSubcomponent = fi.retreiveMessageAddress();
		LENGTH: AADLBA!DataSubcomponent = thisModule.CreateLengthDataSubcomponent(fi);
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.resolveTemp(Sequence{fi.eContainer(), callSequence}, 'returnData');
		TIME_OUT: AADLBA!DataSubcomponent = thisModule.CreateTimeOutSubcomponent(fi);
		spg: AADLBA!SubprogramType = 'Read_Blackboard'.asSubprogramType('ARINC653_RUNTIME');
		resolvedSpg: AADLBA!SubprogramType = thisModule.createResolvedSpg(fi, spg);
	}
	to
		call: AADLBA!SubprogramCallAction
		(
			subprogram <- callHolder,
			parameterLabels <- Sequence{BLACKBOARD_ID_HOLDER,TIMEOUT_HOLDER,MESSAGE_ADDR_HOLDER,LENGTH_HOLDER,RETURN_CODE_HOLDER} 
		),
		callHolder: AADLBA!CalledSubprogramHolder
		(
			element <- resolvedSpg
		),
		BLACKBOARD_ID_HOLDER: AADLBA!DataAccessHolder
		(
			element<-thisModule.resolveTemp(fi, 'f_entrypoint')
		),
		MESSAGE_ADDR_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- MESSAGE_ADDR
		),
		LENGTH_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- LENGTH
		),
		TIMEOUT_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- TIME_OUT
		),
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
}

-- @extends m_BlackBoardCallSpecification,m_Output_DataPort_ComputeEntrypointCallSequence
rule m_Output_BlackBoard_ComputeEntrypointCallSequence
{
	from
		fi: AADLI!FeatureInstance,
		callSequence: AADLI!SubprogramCallSequence,
		cnxRef: AADLI!PortConnection
	using
	{
		MESSAGE_ADDR: AADLBA!DataSubcomponent = fi.retreiveMessageAddress();
		LENGTH: AADLBA!DataSubcomponent = thisModule.CreateLengthDataSubcomponent(fi);
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.resolveTemp(Sequence{fi.eContainer(), callSequence}, 'returnData');
		spg: AADLBA!SubprogramType = 'Display_Blackboard'.asSubprogramType('ARINC653_RUNTIME');
		resolvedSpg: AADLBA!SubprogramType = thisModule.createResolvedSpg(fi, spg);
	}
	to
		call: AADLBA!SubprogramCallAction
		(
			subprogram <- callHolder,
			parameterLabels <- Sequence{BLACKBOARD_ID_HOLDER,MESSAGE_ADDR_HOLDER,LENGTH_HOLDER,RETURN_CODE_HOLDER} 
		),
		callHolder: AADLBA!CalledSubprogramHolder
		(
			element <- resolvedSpg
		),
		BLACKBOARD_ID_HOLDER: AADLBA!DataAccessHolder
		(
			element<-thisModule.resolveTemp(Sequence{fi,cnxRef}, 'f_entrypoint')
		),
		MESSAGE_ADDR_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- MESSAGE_ADDR
		),
		LENGTH_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- LENGTH
		),
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
}
