--
-- AADL-RAMSES
-- 
-- Copyright Â© 2012 TELECOM ParisTech and CNRS
-- 
-- TELECOM ParisTech/LTCI
-- 
-- Authors: see AUTHORS
-- 
-- This program is free software: you can redistribute it and/or modify 
-- it under the terms of the Eclipse Public License as published by Eclipse,
-- either version 1.0 of the License, or (at your option) any later version.
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
-- Eclipse Public License for more details.
-- You should have received a copy of the Eclipse Public License
-- along with this program.  If not, see 
-- http://www.eclipse.org/org/documents/epl-v10.php
--

-- @nsURI AADLBA=/fr.tpt.aadl.annex.behavior/model/aadlba.ecore
-- @atlcompiler emftvm

module SamplingCommunications;
create OUT : AADLBA 	from 		IN : AADLI,
									AADL_RUNTIME: AADLBA,
									DATA_MODEL: AADLBA,
									PROGRAMMING_PROPERTIES: AADLBA,
									HOOKS: ATLHOOKS,

									ARINC653_RUNTIME: AADLBA;


--------------------------------------------
---               SAMPLING               ---
--------------------------------------------

-- @extends m_Not_PeriodicDelayed_Port
abstract rule m_Sampling
{
	from
		fi: AADLI!FeatureInstance,
		behavior: AADLI!Element
		(
			fi.isDataPort()
			and
			not fi.isIntraProcessFeatureInstance()
			and
			(
				behavior.oclIsKindOf(AADLBA!SubprogramCallSequence)
				or
				behavior.oclIsKindOf(AADLBA!BehaviorAnnex)
			)
		)
}

-- @extends m_Sampling
abstract rule m_Input_Sampling
{
	from
		fi: AADLI!FeatureInstance,
		behavior: AADLI!Element
		(
			fi.isInputFeature()
		)
	using
	{
		spg: AADLBA!SubprogramType = 'Read_Sampling_Message'.asSubprogramType('ARINC653_RUNTIME');
		resolvedSpg: AADLBA!SubprogramType = thisModule.createResolvedSpg(fi.feature.dataFeatureClassifier, spg);
		MESSAGE_ADDR: AADLBA!DataSubcomponent = fi.retreiveMessageAddress();
		VALIDITY: AADLBA!DataSubcomponent = thisModule.CreateValidityDataSubcomponent(fi);
		LENGTH: AADLBA!DataSubcomponent = thisModule.CreateLengthDataSubcomponent(fi);
		SAMPLING_ID: AADLBA!DataAccess = fi.getFeatureImg();
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.CreateReturnSubcomponentFromPort(fi);
	}
	to
		call: AADLBA!SubprogramCallAction
		(
			subprogram <- callHolder,
			parameterLabels <- Sequence{SAMPLING_ID_HOLDER,MESSAGE_ADDR_HOLDER,LENGTH_HOLDER,VALIDITY_HOLDER,RETURN_CODE_HOLDER} 
		),
		callHolder: AADLBA!CalledSubprogramHolder
		(
			element <- resolvedSpg
		),
		SAMPLING_ID_HOLDER: AADLBA!DataAccessHolder
		(
			element<-SAMPLING_ID
		),
		MESSAGE_ADDR_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- MESSAGE_ADDR
		),
		LENGTH_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- LENGTH
		),
		VALIDITY_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- VALIDITY
		),
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
}


-- @extends m_Input_Sampling,m_Input_DataPort_ComputeEntrypointCallSequence
rule m_Input_Sampling_ComputeEntrypointCallSequence
{
	from
		fi: AADLI!FeatureInstance,
		behavior: AADLI!SubprogramCallSequence
		(
			behavior.isComputeEntryPointOf(fi.getComponentInstance())
		)
	using
	{
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.CreateReturnSubcomponentFromPort(fi);
	}
	to
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
}

-- @extends m_Input_Sampling,m_Input_Port_BehaviorAnnex
rule m_Input_Sampling_BehaviorAnnex
{
	from
		fi: AADLI!FeatureInstance,
		behavior: AADLBA!AnnexSubclause
	using
	{
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.resolveTemp(fi.getComponentInstance(), 'returnData');
	}
	to
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
}

-- @extends m_Sampling
abstract rule m_Output_Sampling
{
	from
		fi: AADLI!FeatureInstance,
		behavior: AADLI!Element,
		cnxRef: AADLI!PortConnection
		(
			fi.isOutputFeature()
		)
	using
	{
		spg: AADLBA!SubprogramType = 'Write_Sampling_Message'.asSubprogramType('ARINC653_RUNTIME');
		resolvedSpg: AADLBA!SubprogramType = thisModule.createResolvedSpg(fi.feature.dataFeatureClassifier, spg);
		MESSAGE_ADDR: AADLBA!DataSubcomponent = fi.retreiveMessageAddress();
		LENGTH: AADLBA!DataSubcomponent = thisModule.CreateLengthDataSubcomponent(fi);
		SAMPLING_ID: AADLBA!DataAccess = thisModule.resolveTemp(Sequence{fi,cnxRef}, 'f_entrypoint');
	}
	to
		call: AADLBA!SubprogramCallAction
		(
			subprogram <- callHolder,
			parameterLabels <- Sequence{SAMPLING_ID_HOLDER,MESSAGE_ADDR_HOLDER,LENGTH_HOLDER,RETURN_CODE_HOLDER} 
		),
		callHolder: AADLBA!CalledSubprogramHolder
		(
			element <- resolvedSpg
		),
		SAMPLING_ID_HOLDER: AADLBA!DataAccessHolder
		(
			element<-SAMPLING_ID
		),
		MESSAGE_ADDR_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- MESSAGE_ADDR
		),
		LENGTH_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- LENGTH
		),
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
}

-- @extends m_Output_Sampling,m_Output_DataPort_ComputeEntrypointCallSequence
rule m_Output_Sampling_ComputeEntrypointCallSequence
{
	from
		fi: AADLI!FeatureInstance,
		behavior: AADLI!SubprogramCallSequence,
		cnxRef: AADLI!PortConnection
		(
			behavior.isComputeEntryPointOf(fi.getComponentInstance())
		)
	using
	{
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.resolveTemp(fi.getComponentInstance(), 'returnData');
	}
	to
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
}


-- @extends m_Output_Sampling,m_Output_Port_BehaviorAnnex
rule m_Output_Sampling_BehaviorAnnex
{
	from
		fi: AADLI!FeatureInstance,
		behavior: AADLBA!AnnexSubclause,
		cnxRef: AADLI!PortConnection
	using
	{
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.resolveTemp(fi.getComponentInstance(), 'returnData');
	}
	to
		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
}
