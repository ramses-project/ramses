--
-- AADL-RAMSES
-- 
-- Copyright Â© 2012 TELECOM ParisTech and CNRS
-- 
-- TELECOM ParisTech/LTCI
-- 
-- Authors: see AUTHORS
-- 
-- This program is free software: you can redistribute it and/or modify 
-- it under the terms of the Eclipse Public License as published by Eclipse,
-- either version 1.0 of the License, or (at your option) any later version.
-- This program is distributed in the hope that it will be useful,
-- but WITHOUT ANY WARRANTY; without even the implied warranty of
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
-- Eclipse Public License for more details.
-- You should have received a copy of the Eclipse Public License
-- along with this program.  If not, see 
-- http://www.eclipse.org/org/documents/epl-v10.php
--

-- nsURI AADLBA=http:///AADLBA
-- @atlcompiler emftvm

module ExpandThreadsDispatchProtocol;
create OUT : AADLBA 	from 		IN : AADLI,
									AADL_RUNTIME: AADLBA,
									DATA_MODEL: AADLBA,
									PROGRAMMING_PROPERTIES: AADLBA,
									HOOKS: ATLHOOKS,

									RAVENSCAR_RUNTIME: AADLBA;

helper context AADLI!FeatureInstance def: isLastFeatureInstance() : Boolean =
	let c: AADLI!ComponentInstance = self.getComponentInstance() in
	c.featureInstance->last() = self
;

helper def: getLoadedAADLRuntime: String =
	'RAVENSCAR_RUNTIME'
;

helper def: getRuntimeName: String =
	'ravenscar_runtime'
;

helper def: getWaitPeriodSubprogram : AADLBA!SubprogramType = 'Periodic_Wait'.asSubprogramType('RAVENSCAR_RUNTIME');

helper def: getWaitEventSubprogram : AADLBA!SubprogramType = 'Wait_Event'.asSubprogramType('RAVENSCAR_RUNTIME');

rule expandThreadDispatchProtocolInBA(inst: AADLI!ComponentInstance,
									  spg: AADLBA!SubprogramImplementation,
									  actionSequence: AADLBA!BehaviorActionSequence) 
{
 do{
 	if(inst.isPeriodicThread())
 	{
 		actionSequence.actions <- actionSequence.actions->including(thisModule.getPeriodicWaitCallAction(spg));
 	}
 	if(inst.isSporadicThread())
 	{
 		actionSequence.actions <- actionSequence.actions->including(thisModule.getSporadicWaitCallAction(spg));
 	}
 	if(inst.isAperiodicThread())
 	{
 		thisModule.getAperiodicWaitCallAction(spg);
 	}
 }
}

lazy rule getPeriodicWaitCallAction
{
	from
		spgImg: AADLBA!SubprogramImplementation
	using
	{
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.retreiveReturnCodeValue(spgImg);
		spg : AADLBA!SubprogramType = 'Periodic_Wait'.asSubprogramType('RAVENSCAR_RUNTIME');
 	}
	to
		call: AADLBA!SubprogramCallAction
		(
			subprogram <- callHolder,
			parameterLabels <- Sequence{RETURN_CODE_HOLDER} 
		),
		callHolder: AADLBA!CalledSubprogramHolder
		(
			element <- spg
		),

		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
	do
	{
		call;
	}
}

lazy rule getSporadicWaitCallAction
{
	from
		spgImg: AADLBA!SubprogramImplementation
	using
	{
		RETURN_CODE: AADLBA!DataSubcomponent = thisModule.retreiveReturnCodeValue(spgImg);
		spg : AADLBA!SubprogramType = 'Wait_Event'.asSubprogramType('RAVENSCAR_RUNTIME');
 	}
	to
		call: AADLBA!SubprogramCallAction
		(
			subprogram <- callHolder,
			parameterLabels <- Sequence{RETURN_CODE_HOLDER} 
		),
		callHolder: AADLBA!CalledSubprogramHolder
		(
			element <- spg
		),

		RETURN_CODE_HOLDER: AADLBA!DataSubcomponentHolder
		(
			element <- RETURN_CODE
		)
	do
	{
		call;
	}
}