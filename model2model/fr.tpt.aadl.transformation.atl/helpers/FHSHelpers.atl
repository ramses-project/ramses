library FHSHelpers;

helper def : getQuantumConstant() : AADLBA!NamedElement =
	AADLBA!NamedElement->allInstancesFrom('SCHEDULER_CONSTANTS')->any(i|i.name='QUANTUM_VALUE')
;

helper def : getCommonAPISpg(name : String) : AADLBA!Subprogram =
	AADLBA!Subprogram->allInstancesFrom('SCHEDULER_RUNTIME')->any(s|s.name = name)
;

helper context AADLBA!ThreadType def : getInitializeEntrypoint() : AADLBA!Subprogram =
	let asso : AADLBA!PropertyAssociation = self.ownedPropertyAssociation->any(a|a.property.name='Initialize_Entrypoint') in
	if (asso.oclIsUndefined()) then
	    AADLBA!Subprogram->allInstancesFrom('SCHEDULER_REALIZATIONS')->any(s|s.name='Default_Thread_Init')
	else
		asso.ownedValues->first().ownedValue
	endif
;    										

helper def : CurrentTimeSpg() : AADLBA!Subprogram =
	thisModule.getCommonAPISpg('Current_Time')
;

helper context String def : asSchedulingProtocolSubprogram() : AADLBA!Subprogram =
	AADLBA!Subprogram->allInstancesFrom('SCHEDULER_REALIZATIONS')->any(s|s.name=self)
;


helper context AADLI!NamedElement def : getSchedulingProtocol() : String = 
	let asso : AADLI!PropertyAssociation = self.ownedPropertyAssociation->any(a|a.property.name='Scheduling_Protocol') in
	if (asso.oclIsUndefined()) then
		OclUndefined
	else
		asso.ownedValue->first().ownedValue.ownedListElement->first().namedValue.name
	endif
;


helper context AADLI!ComponentInstance def : getSchedulingSubprogram() : AADLBA!Subprogram =
	if (self.category = #processor) then
		AADLBA!Subprogram->allInstancesFrom('SCHEDULER_FHS')->any(s|s.name='root_scheduler.impl')
	else
		if (self.componentInstance->any(c|c.category.toString='virtual processor').oclIsUndefined()) then
			AADLBA!Subprogram->allInstancesFrom('SCHEDULER_FHS')->any(s|s.name='leaf_scheduler.impl')
		else
			AADLBA!Subprogram->allInstancesFrom('SCHEDULER_FHS')->any(s|s.name='node_scheduler.impl')
		endif
	endif
;